
&НаКлиенте
Процедура ВИЛС_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ДокументооборотБизнесПроцесс" Тогда
		ПараметрыВозврата = УстановитьСтатусДокумента(этаФорма.ВнешнийОбъект);
		Если Не ПараметрыВозврата = Неопределено Тогда
			 Оповестить("ИзменитьСтатус",ПараметрыВозврата,этаФорма.ВладелецФормы);
			 ИзменитьДоступностьСозданиеЗадачи();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция УстановитьСтатусДокумента(СсылкаНаОбъект)
		
	Попытка
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Исключение
		Возврат Неопределено; // Если нет соединения, вернем статус Ложь 
	КонецПопытки;
	
	Если Прокси = Неопределено Тогда
		Возврат Неопределено; // Если пользователь не авторизован в ДО, вернем Ложь.
	КонецЕсли;
	
	ExternalObject = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
	ExternalObject.id = Строка(СсылкаНаОбъект.УникальныйИдентификатор());
	ExternalObject.type = СсылкаНаОбъект.Метаданные().ПолноеИмя();
	ExternalObject.name = Строка(СсылкаНаОбъект);
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetDocumentListRequest");
	Запрос.externalObjects.Добавить(ExternalObject);
	
	Запрос.columnSet.Добавить("status");
	Запрос.columnSet.Добавить("statusApproval");
	Запрос.columnSet.Добавить("statusConfirmation");
	Запрос.columnSet.Добавить("statusRegistration");
	Запрос.columnSet.Добавить("statusConsideration");
	Запрос.columnSet.Добавить("statusPerformance");
	Запрос.columnSet.Добавить("visas");
	
	Результат = Прокси.execute(Запрос);
	
	Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, Результат, "DMError") Тогда 
		Возврат Неопределено; // Произошла ошибка во время выполнения запроса
	КонецЕсли;	
		
	Если Результат.documents.Количество() > 0 Тогда 
		ОбъектВозврата = Результат.documents[0];
	Иначе 
		Возврат Неопределено; // Нужного документа не оказалось
	КонецЕсли;

	Согласован = Ложь;
	
	Если  не ОбъектВозврата.statusApproval = Неопределено Тогда
	
		 Согласован = ОбъектВозврата.statusApproval.name = "Согласован";
	
	КонецЕсли;
	
	Утвержден = Неопределено;
	
	Если  не ОбъектВозврата.statusConfirmation = Неопределено Тогда
		 Утвержден = ?(ОбъектВозврата.statusConfirmation.name = "На утверждении",Неопределено,ОбъектВозврата.statusConfirmation.name = "Утвержден");
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура("Согласован,Утвержден",Согласован,Утвержден);
	
	Возврат СтруктураВозврата;
КонецФункции

&НаСервере
Процедура ВИЛС_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	ИзменитьДоступностьСозданиеЗадачи();
КонецПроцедуры

&НаСервере
Процедура ИзменитьДоступностьСозданиеЗадачи()
	
	Если этаФорма.ВнешнийОбъект.Статус = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.Отклонена 
		или этаФорма.ВнешнийОбъект.Статус = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате или не этаФорма.ВнешнийОбъект.Проведен Тогда
		Элементы.ДеревоБизнесПроцессовИЗадачСоздатьБизнесПроцесс.Доступность = Ложь;
	КонецЕсли;	
КонецПроцедуры

