//>>fix бесшовка
// Записывает задачу, возможно, выполняя ее.
//
// Параметры:
//   РезультатВыполненияЗадачи - Неопределено, если задача просто записывается, или
//                       - Структура - результат выполнения со свойствами:
//     НомерКнопки - Число - номер кнопки, выполняющей задачу.
//     ВыбранныйИсполнитель - Строка - currentUser - текущий пользователь, или
//                                   - taskPerformer - фактический исполнитель задачи.
//     ПредметСсылка - ЛюбаяСсылка - неявно возвращаемый параметр, предмет задачи,
//       требующий оповещения об изменении.
//
&ИзменениеИКонтроль("ЗаписатьОбъект")
&НаСервере
Функция ВИЛС_ЗаписатьОбъект(РезультатВыполненияЗадачи = Неопределено)
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ПроцессXDTO = СоздатьОбъект(Прокси, ПроцессТип);
	ПроцессXDTO.name = Процесс;
	ПроцессXDTO.objectID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ПроцессID, ПроцессТип);
	
	Задача = СоздатьОбъект(Прокси, Тип);
	
	Если ДоступнаМультипредметность Тогда
		ЗаполнитьЗадачуПредметамиИзДереваПриложений(Прокси, ДеревоПриложений.ПолучитьЭлементы(), Задача);
	КонецЕсли;
	
	Задача.executed = ?(РезультатВыполненияЗадачи <> Неопределено, Истина, Выполнена);
	Задача.parentBusinessProcess = ПроцессXDTO;
	
	ЗаполнитьСвойстваОбъектаПоЗадаче(Задача, Прокси);
	ЗаполнитьXDTOПоОбъектномуРеквизиту(Прокси, "Важность", "importance", Задача);
	
	// Заполним результат и исполнителя задачи при выполнении.
	Если РезультатВыполненияЗадачи <> Неопределено Тогда
		
		ЗаполнитьСвойстваОбъектаПоТипуЗадачиПриВыполнении(Прокси, Задача, РезультатВыполненияЗадачи.НомерКнопки);
		
		Если РезультатВыполненияЗадачи.ВыбранныйИсполнитель = "currentUser" Тогда
			Обработки.ИнтеграцияС1СДокументооборот.СоздатьУчастникаБизнесПроцесса(
				Прокси, Задача.performer, ЭтаФорма, "ТекущийПользователь", "ОбъектАдресации");
		Иначе
			Обработки.ИнтеграцияС1СДокументооборот.СоздатьУчастникаБизнесПроцесса(
				Прокси, Задача.performer, ЭтаФорма, "Исполнитель", "ОбъектАдресации");
		КонецЕсли;
		
	Иначе // обычная запись
		
		Обработки.ИнтеграцияС1СДокументооборот.СоздатьУчастникаБизнесПроцесса(
			Прокси, Задача.performer, ЭтаФорма, "Исполнитель", "ОбъектАдресации");
		
	КонецЕсли;
	
	Обработки.ИнтеграцияС1СДокументооборот.СформироватьДополнительныеСвойства(Прокси, Задача, ЭтаФорма);
	
	Ответ = ИнтеграцияС1СДокументооборот.ЗаписатьОбъект(Прокси, Задача);
	
	Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, Ответ, "DMError") Тогда
		
		Возврат Ложь;
		
	Иначе // Изменение состояния согласования требует особой обработки с оповещением предмета.
		
		Если Тип = "DMBusinessProcessApprovalTaskApproval"
			И РезультатВыполненияЗадачи <> Неопределено Тогда
			
			НовоеСостояние = Неопределено;
			Если РезультатВыполненияЗадачи.НомерКнопки = 3 Тогда
				НовоеСостояние = Перечисления.СостоянияСогласованияВДокументообороте.НеСогласован;
			Иначе
				ДанныеПроцесса = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси,
					ПроцессТип,
					ПроцессID,
					СтрРазделить("completed", ""));
#Удаление
				Если ДанныеПроцесса.objects[0].completed = Истина Тогда
					НовоеСостояние = Перечисления.СостоянияСогласованияВДокументообороте.Согласован;
				КонецЕсли;
#КонецУдаления
#Вставка
				Если ДанныеПроцесса.objects[0].completed = Истина Тогда
					НовоеСостояние = Перечисления.СостоянияСогласованияВДокументообороте.Согласован;
				Иначе
					Для Каждого СтрокаПредмета Из ДеревоПриложений.ПолучитьЭлементы() Цикл
						Если (Не ДоступнаМультипредметность Или СтрокаПредмета.РольПредмета = "Основной")
								И ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(СтрокаПредмета.ПредметТип) Тогда
								Запрос = Новый Запрос(
								"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
								|	Объект КАК ПредметСогласования
								|ИЗ 
								|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
								|ГДЕ 
								|	ТипОбъектаДО = &Тип
								|	И ИдентификаторОбъектаДО = &Идентификатор
								|");
								Запрос.УстановитьПараметр("Тип", СтрокаПредмета.ПредметТип);
								Запрос.УстановитьПараметр("Идентификатор", СтрокаПредмета.ПредметID);
								Выборка = Запрос.Выполнить().Выбрать();
								Если Выборка.Следующий() Тогда
									ПредметСогласования = Выборка.ПредметСогласования;
									ИнтеграцияС1СДокументооборотПереопределяемый.ПриИзмененииСостоянияСогласования(ПредметСогласования,НовоеСостояние,Ложь);
								КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
#КонецВставки
			КонецЕсли;
			Если НовоеСостояние <> Неопределено Тогда
				Для Каждого СтрокаПредмета Из ДеревоПриложений.ПолучитьЭлементы() Цикл
					Если (Не ДоступнаМультипредметность Или СтрокаПредмета.РольПредмета = "Основной")
						И ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(СтрокаПредмета.ПредметТип) Тогда
						ИнтеграцияС1СДокументооборотВызовСервера.ПриИзмененииСостоянияСогласования(
							СтрокаПредмета.ПредметID,
							СтрокаПредмета.ПредметТип,
							НовоеСостояние,
							Ложь,
							РезультатВыполненияЗадачи.ПредметСсылка);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
#Вставка		
		Если Тип = "DMBusinessProcessConfirmationTaskConfirmation"  
			И РезультатВыполненияЗадачи <> Неопределено Тогда
			НовоеСостояние = Неопределено;
			Если РезультатВыполненияЗадачи.НомерКнопки = 3 Тогда
				НовоеСостояние = "Отклонено";
			Иначе
				НаборКолонок = Новый Массив;
				НаборКолонок.Добавить("completed");
				ДанныеПроцесса = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси,
				ПроцессТип,
				ПроцессID,
				СтрРазделить("completed", ""));
				Если ДанныеПроцесса.objects[0].completed = Истина Тогда
					НовоеСостояние = "Утверждено";
				КонецЕсли;
			КонецЕсли;
			Если НовоеСостояние <> Неопределено Тогда
				Для Каждого СтрокаПредмета Из ДеревоПриложений.ПолучитьЭлементы() Цикл
					Если (Не ДоступнаМультипредметность Или СтрокаПредмета.РольПредмета = "Основной")
						И ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(СтрокаПредмета.ПредметТип) Тогда
						ИнтеграцияС1СДокументооборотВызовСервера.ВИЛС_ПриИзмененииУтверждения(
						СтрокаПредмета.ПредметID,
						СтрокаПредмета.ПредметТип,
						НовоеСостояние,
						Ложь,
						РезультатВыполненияЗадачи.ПредметСсылка);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
#КонецВставки
			
		Модифицированность = Ложь;
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции
//<<fix бесшовка