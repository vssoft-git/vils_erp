
&НаСервере
Процедура ВИЛС_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	//Реквизиты
	Реквизит = Новый РеквизитФормы("ВИЛС_НомерКонтракта",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(10)),,"Номер контракта");
	МассивДобавляемых = Новый Массив;
	МассивДобавляемых.Добавить(Реквизит);
	Реквизит = Новый РеквизитФормы("ВИЛС_УсловиеТруда",Новый ОписаниеТипов("СправочникСсылка.УсловияТруда"),,"Номер контракта");
	МассивДобавляемых.Добавить(Реквизит);
	ИзменитьРеквизиты(МассивДобавляемых);
	
	//Команды
	НоваяКоманда = Команды.Добавить("ВИЛС_ОткрытьКарточку");
	НоваяКоманда.Действие = "ОткрытьУсловиеТруда";
	НоваяКоманда = Команды.Добавить("ВИЛС_ОткрытьИсториюУсловийТруда");
	НоваяКоманда.Действие = "ОткрытьИсториюУсловийТруда";
	
	//Элементы
	НовыйЭлемент = Элементы.Вставить("ВИЛС_НомерКонтракта",Тип("ПолеФормы"),Элементы.ДатыПриемаУвольнения,Элементы.ДатаПриема);
	НовыйЭлемент.ПутьКДанным = "ВИЛС_НомерКонтракта";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.Ширина = 6;
	НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
	
	НоваяДекорация = Элементы.Добавить("ВИЛС_ЗаголовокУсловийТруда",Тип("ДекорацияФормы"),Элементы.СостояниеСотрудника);
	НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
	НоваяДекорация.Заголовок = "Условия труда";
	
	НоваяКнопка = Элементы.Добавить("ВИЛС_УсловиеТруда",Тип("КнопкаФормы"),Элементы.СостояниеСотрудника);
	НоваяКнопка.Вид = ВидКнопкиФормы.Гиперссылка;
    НоваяКнопка.ИмяКоманды = "ВИЛС_ОткрытьКарточку";
	
	КнопкаИстории = Элементы.Добавить("ВИЛС_ОткрытьИсториюУсловийТруда",Тип("КнопкаФормы"),Элементы.СостояниеСотрудника);
	КнопкаИстории.Вид = ВидКнопкиФормы.Гиперссылка;
    КнопкаИстории.ИмяКоманды = "ВИЛС_ОткрытьИсториюУсловийТруда";
	КнопкаИстории.Заголовок = "история...";
	
	Запрос = новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеДанныеКонтрактаДоговораСотрудникаСрезПоследних.НомерДоговораКонтракта КАК Номер
	|ИЗ
	|	РегистрСведений.ОсновныеДанныеКонтрактаДоговораСотрудника.СрезПоследних(, Сотрудник = &Сотрудник) КАК ОсновныеДанныеКонтрактаДоговораСотрудникаСрезПоследних";
	Запрос.УстановитьПараметр("Сотрудник",Сотрудник.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ЭтотОбъект["ВИЛС_НомерКонтракта"] = Выборка.Номер;
	
	Запрос = новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВИЛС_ДанныеУсловийТрудаПоСотрудникамСрезПоследних.УсловияТруда КАК УсловияТруда
	|ИЗ
	|	РегистрСведений.ВИЛС_ДанныеУсловийТрудаПоСотрудникам.СрезПоследних(, Сотрудник = &Сотрудник) КАК ВИЛС_ДанныеУсловийТрудаПоСотрудникамСрезПоследних";
	Запрос.УстановитьПараметр("Сотрудник",Сотрудник.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ЭтотОбъект["ВИЛС_УсловиеТруда"] = Выборка.УсловияТруда;
	
	НоваяКнопка.Заголовок = СокрЛП(?(Выборка.УсловияТруда=NULL,"<история отсутсвует>",Выборка.УсловияТруда));
КонецПроцедуры

&Вместо("КадровыеДокументы")
&НаКлиенте
Процедура ВИЛС_КадровыеДокументы(Команда)
	
	СтандартнаяОбработка = Истина;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ГосударственнаяСлужбаКлиент");
		Модуль.ОткрытьФормуКадровыхПриказовВоеннослужащих(ЭтотОбъект, СтандартнаяОбработка);
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		ОписаниеФормы = СотрудникиКлиентСервер.ОбщееОписаниеДополнительнойФормы("ОбщаяФорма.ВИЛС_ВсеКадровыеДокументы");
		ОписаниеФормы.КлючевыеРеквизиты.Вставить("СотрудникСсылка");
		ОписаниеФормы.КлючевыеРеквизиты.Вставить("Заголовок", "Сотрудник.Наименование");
		//СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("ЖурналДокументов.ВИЛС_ВсеКадровыеДокументы")
		
		СотрудникиКлиент.ОткрытьДополнительнуюФорму(ОписаниеФормы,ЭтотОбъект, Истина);
		//ОткрытьФорму("ОбщаяФорма.ВИЛС_ВсеКадровыеДокументы")
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУсловиеТруда(Кнопка)
	ПоказатьЗначение(,ЭтотОбъект["ВИЛС_УсловиеТруда"]);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюУсловийТруда(Кнопка)
	ПараметрыФормы = Новый Структура("Сотрудник",Сотрудник.Ссылка);
	ОткрытьФорму("Справочник.Сотрудники.Форма.ВИЛС_ИсторияУсловий",ПараметрыФормы);
КонецПроцедуры