
&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура ВИЛС_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
#Удаление

	СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	СтатьяРасходовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();

	СтатьяРасходовВзносов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	СтатьяРасходовВзносовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();

	СтатьяРасходовТравматизм = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();

#КонецУдаления
#Вставка
	Если Не ЭтоГруппа Тогда

		СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
		СтатьяРасходовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();

		СтатьяРасходовВзносов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
		СтатьяРасходовВзносовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();

		СтатьяРасходовТравматизм = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();

	КонецЕсли;
#КонецВставки	
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура ВИЛС_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
#Вставка
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
#КонецВставки	

	НеИспользуемыеРеквизиты = Справочники.СпособыОтраженияЗарплатыВБухУчете.НеИспользуемыеРеквизиты(ЭтотОбъект);

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
	ПроверяемыеРеквизиты,
	НеИспользуемыеРеквизиты);

	МассивНепроверяемыхРеквизитов = Новый Массив;

	ПодбираемыеАналитики = Новый Массив;
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.Организации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	ПодбираемыеАналитики.Добавить(Тип("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ОбъектыЭксплуатации"));

	Если ПроверяемыеРеквизиты.Найти("АналитикаРасходов") <> Неопределено И ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходов)) = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
		ЭтотОбъект, "СтатьяРасходов, АналитикаРасходов", МассивНепроверяемыхРеквизитов, Отказ);
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходов");
	КонецЕсли;

	Если ПроверяемыеРеквизиты.Найти("АналитикаРасходовВзносов") <> Неопределено И ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовВзносов)) = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
		ЭтотОбъект, "СтатьяРасходовВзносов, АналитикаРасходовВзносов", МассивНепроверяемыхРеквизитов, Отказ);
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносов");
	КонецЕсли;

	Если ПроверяемыеРеквизиты.Найти("АналитикаРасходовЕНВД") <> Неопределено И ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовЕНВД)) = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
		ЭтотОбъект, "СтатьяРасходовЕНВД, АналитикаРасходовЕНВД", МассивНепроверяемыхРеквизитов, Отказ);
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
	КонецЕсли;

	Если ПроверяемыеРеквизиты.Найти("АналитикаРасходовВзносовЕНВД") <> Неопределено И ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовВзносовЕНВД)) = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
		ЭтотОбъект, "СтатьяРасходовВзносовЕНВД, АналитикаРасходовВзносовЕНВД", МассивНепроверяемыхРеквизитов, Отказ);
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносовЕНВД");
	КонецЕсли;

	Если  ПроверяемыеРеквизиты.Найти("АналитикаРасходовТравматизм") <> Неопределено И ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовТравматизм)) = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
		ЭтотОбъект, "СтатьяРасходовТравматизм, АналитикаРасходовТравматизм", МассивНепроверяемыхРеквизитов, Отказ);
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовТравматизм");
	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
	ПроверяемыеРеквизиты,
	МассивНепроверяемыхРеквизитов);

КонецПроцедуры

&ИзменениеИКонтроль("ПередЗаписью")
Процедура ВИЛС_ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
#Вставка
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
#КонецВставки	

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ЕстьПрочиеОперации = Справочники.СпособыОтраженияЗарплатыВБухУчете.ОтражениеПрочихОпераций(ЭтотОбъект);

	// очистка неиспользуемых реквизитов
	НеИспользуемыеРеквизиты = Справочники.СпособыОтраженияЗарплатыВБухУчете.НеИспользуемыеРеквизиты(ЭтотОбъект);

	Для Каждого Реквизит Из НеИспользуемыеРеквизиты Цикл
		ЭтотОбъект[Реквизит] = Неопределено;
	КонецЦикла;

	// установка значений по умолчанию

	Если Не ЗначениеЗаполнено(БазаРаспределенияПоСдельнымРаботам) Тогда
		БазаРаспределенияПоСдельнымРаботам = Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда;
	КонецЕсли;

	Если СтатьяРасходов = Неопределено Тогда
		СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	КонецЕсли;
	ОписаниеТипов = Новый ОписаниеТипов(СтатьяРасходов.ТипЗначения);
	АналитикаРасходов = ОписаниеТипов.ПривестиЗначение(АналитикаРасходов);
	АналитикаАктивовПассивов = ОписаниеТипов.ПривестиЗначение(АналитикаАктивовПассивов);

	Если СтатьяРасходовЕНВД = Неопределено Тогда
		СтатьяРасходовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	КонецЕсли;
	ОписаниеТипов = Новый ОписаниеТипов(СтатьяРасходовЕНВД.ТипЗначения);
	АналитикаРасходовЕНВД = ОписаниеТипов.ПривестиЗначение(АналитикаРасходовЕНВД);
	АналитикаАктивовПассивовЕНВД = ОписаниеТипов.ПривестиЗначение(АналитикаАктивовПассивовЕНВД);

	Если СтатьяРасходовТравматизм = Неопределено Тогда
		СтатьяРасходовТравматизм = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	КонецЕсли;
	ОписаниеТипов = Новый ОписаниеТипов(СтатьяРасходовЕНВД.ТипЗначения);
	АналитикаРасходовТравматизм = ОписаниеТипов.ПривестиЗначение(АналитикаРасходовТравматизм);
	АналитикаАктивовПассивовТравматизм = ОписаниеТипов.ПривестиЗначение(АналитикаАктивовПассивовТравматизм);

	Если РежимУчетаВзносов = 0 Тогда
		СтатьяРасходовВзносов = СтатьяРасходов;
		АналитикаРасходовВзносов = АналитикаРасходов;
		АналитикаАктивовПассивовВзносов = АналитикаАктивовПассивов;
		СчетУчетаВзносов = СчетУчета;
		Субконто1Взносов = Субконто1;
		Субконто2Взносов = Субконто2;
		Субконто3Взносов = Субконто3;
	КонецЕсли;

	Если ОсобыйСоответствуетОсновному Тогда

		СтатьяРасходовЕНВД = СтатьяРасходов;
		АналитикаРасходовЕНВД = АналитикаРасходов;
		АналитикаАктивовПассивовЕНВД = АналитикаАктивовПассивов;
		СчетУчетаЕНВД = СчетУчета;
		Субконто1ЕНВД = Субконто1;
		Субконто2ЕНВД = Субконто2;
		Субконто3ЕНВД = Субконто3;

		СтатьяРасходовВзносовЕНВД = СтатьяРасходовВзносов;
		АналитикаРасходовВзносовЕНВД = АналитикаРасходовВзносов;
		АналитикаАктивовПассивовВзносовЕНВД = АналитикаАктивовПассивовВзносов;
		СчетУчетаВзносовЕНВД = СчетУчетаВзносов;
		Субконто1ВзносовЕНВД = Субконто1Взносов;
		Субконто2ВзносовЕНВД = Субконто2Взносов;
		Субконто3ВзносовЕНВД = Субконто3Взносов;

	ИначеЕсли РежимУчетаВзносовЕНВД = 0 Тогда

		СтатьяРасходовВзносовЕНВД = СтатьяРасходовЕНВД;
		АналитикаРасходовВзносовЕНВД = АналитикаРасходовЕНВД;
		АналитикаАктивовПассивовВзносовЕНВД = АналитикаАктивовПассивовЕНВД;
		СчетУчетаВзносовЕНВД = СчетУчетаЕНВД;
		Субконто1ВзносовЕНВД = Субконто1ЕНВД;
		Субконто2ВзносовЕНВД = Субконто2ЕНВД;
		Субконто3ВзносовЕНВД = Субконто3ЕНВД;

	КонецЕсли;

КонецПроцедуры
