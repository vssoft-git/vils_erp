#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ИзменитьЗаписиПоРегистратору(Документ,Добавить = Ложь)Экспорт
	Сотрудники = СотрудникиДокумента(Документ);	
	 
	Для Каждого СтрокаСотрудников ИЗ Сотрудники Цикл
		Набор = РегистрыСведений.ВИЛС_ДанныеУсловийТрудаПоСотрудникам.СоздатьНаборЗаписей();
		Набор.Отбор.Период.Установить(СтрокаСотрудников.ДатаСобытия);
		Набор.Отбор.Сотрудник.Установить(СтрокаСотрудников.Сотрудник);
		
		Набор.Прочитать();
		Набор.Очистить();
		Если Добавить Тогда
			НоваяЗапись = Набор.Добавить();
			НоваяЗапись.Период = СтрокаСотрудников.ДатаСобытия;
			НоваяЗапись.Сотрудник = СтрокаСотрудников.Сотрудник;
			НоваяЗапись.УсловияТруда = СтрокаСотрудников.ВИЛС_УсловияТруда;
			НоваяЗапись.ДокументРегистратор=Документ;
		КонецЕсли;
		Набор.Записать();
	КонецЦикла;
КонецПроцедуры	

Функция СотрудникиДокумента(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
		|	ПриемНаРаботу.Сотрудник КАК Сотрудник,
		|	ПриемНаРаботу.ДатаПриема КАК ДатаСобытия,
		|	ПриемНаРаботу.Ссылка КАК Регистратор,
		|	ПриемНаРаботу.ВИЛС_УсловияТруда КАК ВИЛС_УсловияТруда
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|ГДЕ
		|	ПриемНаРаботу.Ссылка = &Ссылка
		|	И НЕ ПриемНаРаботу.ВИЛС_УсловияТруда = ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриемНаРаботуСпискомСотрудники.Сотрудник,
		|	ПриемНаРаботуСпискомСотрудники.ДатаПриема,
		|	ПриемНаРаботуСпискомСотрудники.Ссылка,
		|	ПриемНаРаботуСпискомСотрудники.ВИЛС_УсловияТруда
		|ИЗ
		|	Документ.ПриемНаРаботуСписком.Сотрудники КАК ПриемНаРаботуСпискомСотрудники
		|ГДЕ
		|	ПриемНаРаботуСпискомСотрудники.Ссылка = &Ссылка
		|	И НЕ ПриемНаРаботуСпискомСотрудники.ВИЛС_УсловияТруда = ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыйПеревод.Сотрудник,
		|	КадровыйПеревод.ДатаНачала,
		|	КадровыйПеревод.Ссылка,
		|	КадровыйПеревод.ВИЛС_УсловияТруда
		|ИЗ
		|	Документ.КадровыйПеревод КАК КадровыйПеревод
		|ГДЕ
		|	КадровыйПеревод.Ссылка = &Ссылка
		|	И НЕ КадровыйПеревод.ВИЛС_УсловияТруда = ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыйПереводСпискомСотрудники.Сотрудник,
		|	КадровыйПереводСпискомСотрудники.ДатаНачала,
		|	КадровыйПереводСпискомСотрудники.Ссылка,
		|	КадровыйПереводСпискомСотрудники.ВИЛС_УсловияТруда
		|ИЗ
		|	Документ.КадровыйПереводСписком.Сотрудники КАК КадровыйПереводСпискомСотрудники
		|ГДЕ
		|	КадровыйПереводСпискомСотрудники.Ссылка = &Ссылка
		|	И НЕ КадровыйПереводСпискомСотрудники.ВИЛС_УсловияТруда = ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Увольнение.Сотрудник,
		|	Увольнение.ДатаУвольнения,
		|	Увольнение.Ссылка,
		|	Увольнение.ВИЛС_УсловияТруда
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.Ссылка = &Ссылка
		|	И НЕ Увольнение.ВИЛС_УсловияТруда = ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УвольнениеСпискомСотрудники.Сотрудник,
		|	УвольнениеСпискомСотрудники.ДатаУвольнения,
		|	УвольнениеСпискомСотрудники.Ссылка,
		|	УвольнениеСпискомСотрудники.ВИЛС_УсловияТруда
		|ИЗ
		|	Документ.УвольнениеСписком.Сотрудники КАК УвольнениеСпискомСотрудники
		|ГДЕ
		|	УвольнениеСпискомСотрудники.Ссылка = &Ссылка
		|	И НЕ УвольнениеСпискомСотрудники.ВИЛС_УсловияТруда = ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НачальнаяШтатнаяРасстановка.Сотрудник,
		|	КОНЕЦПЕРИОДА(НачальнаяШтатнаяРасстановка.Ссылка.Месяц,МЕСЯЦ),
		|	НачальнаяШтатнаяРасстановка.Ссылка,
		|	НачальнаяШтатнаяРасстановка.ВИЛС_УсловияТруда
		|ИЗ
		|	Документ.НачальнаяШтатнаяРасстановка.Сотрудники КАК НачальнаяШтатнаяРасстановка
		|ГДЕ
		|	НачальнаяШтатнаяРасстановка.Ссылка = &Ссылка
		|	И НЕ НачальнаяШтатнаяРасстановка.ВИЛС_УсловияТруда = ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)
		|";
	Запрос.УстановитьПараметр("Ссылка",Документ);
	Возврат(Запрос.Выполнить().Выгрузить());
	
КонецФункции

#КонецЕсли