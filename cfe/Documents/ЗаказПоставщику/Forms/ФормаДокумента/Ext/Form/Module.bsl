
&НаСервере
&Вместо("ДоговорПриИзмененииСервер")
Процедура ВИЛС_ДоговорПриИзмененииСервер()
	
	ЗакупкиВызовСервера.ЗаполнитьБанковскиеСчетаПоДоговору(Объект.Договор, Объект.БанковскийСчет);
	ОбновитьФорму = Ложь;
	ВзаиморасчетыСервер.ЗаполнитьПорядокРасчетовВФорме(ЭтаФорма, Ложь, , ОбновитьФорму);
	Если ОбновитьФорму Тогда
		УстановитьСвойстваЭлементовПоПорядкуРасчетов();
	КонецЕсли;
	ЗаполнитьГруппуФинУчета();

	Если ИспользоватьНаправленияДеятельности Тогда
		НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(Объект.НаправлениеДеятельности, Объект.Соглашение, Объект.Договор);
	КонецЕсли;
	
	ЗаполнитьНалогообложениеНДСЗакупки();
	НалогообложениеНДСПриИзмененииСервер(Неопределено);
	
	ЗаполнитьВидДеятельностиНДС();
	
	ЗаполнитьПодчиненныеСвойстваПоСтатистике("Договор");
	ПриИзмененииНаправленияДеятельностиСервер();
	
	Если ЗначениеЗаполнено(Объект.Договор) Тогда
		Объект.ПорядокОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор,"ПорядокОплаты");
	Иначе
		ЗаполнитьПорядокОплатыПоУмолчанию();
	КонецЕсли;
	
	Если Договор <> Объект.Договор Тогда
		Договор = Объект.Договор;
		Объект.СпособДоставки = Договор.СпособДоставки;     // fix Suetin 22.08.2019 12:03:21
		Объект.ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантПриемкиТоваров(Неопределено, Объект.Договор);
	КонецЕсли;
	
	ДоставкаТоваров.ПриЧтенииСозданииРаспоряженийНаСервере(Элементы, Объект,,,СводнаяИнформацияПоДоставке);
	
	Если Элементы.СтраницаДоставка.Видимость Тогда          // begin fix Suetin 22.08.2019 12:04:09
		Если Элементы.ПеревозчикПартнер.Видимость 
			и Не ЗначениеЗаполнено(Объект.ПеревозчикПартнер) Тогда
			Объект.ПеревозчикПартнер 						= Договор.ПеревозчикПартнер;
		КонецЕсли;
		Если Элементы.АдресДоставкиПеревозчика.Видимость 
			и Не ЗначениеЗаполнено(Объект.АдресДоставкиПеревозчика) Тогда
			Объект.АдресДоставкиПеревозчика 				= Договор.АдресДоставкиПеревозчика;
			Объект.АдресДоставкиПеревозчикаЗначенияПолей 	= Договор.АдресДоставкиПеревозчикаЗначенияПолей;
		ИначеЕсли Элементы.АдресПоставщика.Видимость 
			и Не ЗначениеЗаполнено(Объект.АдресДоставки) Тогда
			Объект.АдресДоставки 							= Договор.АдресДоставки;
			Объект.АдресДоставкиЗначенияПолей 				= Договор.АдресДоставкиЗначенияПолей;
		ИначеЕсли Элементы.АдресДоставкиДляПоставщика.Видимость 
			и Не ЗначениеЗаполнено(Объект.АдресДоставкиДляПоставщика) Тогда
			Объект.АдресДоставкиДляПоставщика 				= Договор.АдресДоставкиДляПоставщика;
		КонецЕсли;           
		Если Элементы.ЗонаДоставки.Видимость 
			и Не ЗначениеЗаполнено(Объект.ЗонаДоставки) Тогда
			Объект.ЗонаДоставки 							= Договор.ЗонаДоставки;
			Объект.ВремяДоставкиС 							= Договор.ВремяДоставкиС;
			Объект.ВремяДоставкиПо 							= Договор.ВремяДоставкиПо;
		КонецЕсли;
		Если Элементы.ДополнительнаяИнформацияПоДоставке.Видимость 
			и Не ЗначениеЗаполнено(Объект.ДополнительнаяИнформацияПоДоставке) Тогда
			Объект.ДополнительнаяИнформацияПоДоставке 		= Договор.ДополнительнаяИнформацияПоДоставке;
		КонецЕсли;
	КонецЕсли;	                                            // end fix Suetin 22.08.2019 12:04:15

	
	ПриИзмененииКлючевыхРеквизитовСостояниеЭДО();
	
КонецПроцедуры

&НаСервере
Процедура ВИЛС_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ЗаполнитьУстановитьРеквизитыДоставкиСервер("СпособДоставки");
	КонецЕсли;	
	// begin fix Suetin 16.10.2019 4:09:52
	Элементы.ПеревозчикПартнер.АвтоОтметкаНезаполненного 			= Ложь;
	Элементы.АдресДоставкиДляПоставщика.АвтоОтметкаНезаполненного 	= Ложь;
	Элементы.АдресДоставкиПеревозчика.АвтоОтметкаНезаполненного   	= Ложь;
	Элементы.АдресПоставщика.АвтоОтметкаНезаполненного   			= Ложь;
	// end fix Suetin 16.10.2019 4:09:59
КонецПроцедуры
