
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ВИЛС_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Элементы.ДокументыОснованияДокументОснование.Видимость = Ложь;
	
	ДобавляемыйЭлемент = Элементы.Вставить("ВИЛС_ДокументОснование", Тип("ПолеФормы"), Элементы.ДокументыОснования);
	ДобавляемыйЭлемент.Вид 						= ВидПоляФормы.ПолеВвода; 
   	ДобавляемыйЭлемент.ПутьКДанным 				= "Объект.ДокументыОснования.ВИЛС_ДокументОснование";
	ДобавляемыйЭлемент.КнопкаВыбора				= Истина;
	ДобавляемыйЭлемент.ОтображениеКнопкиВыбора	= ОтображениеКнопкиВыбора.ОтображатьВПолеВвода;
	ДобавляемыйЭлемент.КнопкаОткрытия			= Истина;
	ДобавляемыйЭлемент.КнопкаВыпадающегоСписка	= Ложь;
	ДобавляемыйЭлемент.Заголовок				= "Документ основание (ВИЛС)";
	ДобавляемыйЭлемент.УстановитьДействие("ПриИзменении", "ВИЛС_ДокументОснованиеПриИзменении");
	Элементы.Грузополучатель.УстановитьДействие("ПриИзменении", "ВИЛС_ГрузополучательПриИзмененииПосле");
	Элементы.Грузоотправитель.УстановитьДействие("ПриИзменении", "ВИЛС_ГрузоотправительПриИзмененииПосле");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВИЛС_ГрузополучательПриИзмененииПосле(Элемент)
	ВИЛС_ГрузополучательПриИзмененииПослеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВИЛС_ГрузоотправительПриИзмененииПосле(Элемент)
	ВИЛС_ГрузоотправительПриИзмененииПослеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВИЛС_ГрузополучательПриИзмененииПослеНаСервере()
	СтруктураАдреса = ВИЛС_ГрузоотправительГрузополучательПриИзмененииНаСервере(Объект.Грузополучатель);
	Объект.АдресДоставки              = СтруктураАдреса.Представление;
	Объект.АдресДоставкиЗначенияПолей = СтруктураАдреса.ЗначенияПолей;
КонецПроцедуры

&НаСервере
Процедура ВИЛС_ГрузоотправительПриИзмененииПослеНаСервере()
	СтруктураАдреса = ВИЛС_ГрузоотправительГрузополучательПриИзмененииНаСервере(Объект.Грузоотправитель);
	Объект.АдресПогрузки              = СтруктураАдреса.Представление;
	Объект.АдресПогрузкиЗначенияПолей = СтруктураАдреса.ЗначенияПолей;
КонецПроцедуры

&НаСервереБезКонтекста
//&После("ГрузоотправительПриИзмененииНаСервере")
Функция ВИЛС_ГрузоотправительГрузополучательПриИзмененииНаСервере(ОбъектКонтактнойИнформации)
	СтруктураАдреса = Новый Структура("Представление, ЗначенияПолей", "", "");
	Если ТипЗнч(ОбъектКонтактнойИнформации) = Тип("СправочникСсылка.Контрагенты") Тогда
		КонтрагентаОрганизации 	= "Контрагента";	
		СправочникКО 			= "Контрагенты";
	Иначе
		КонтрагентаОрганизации 	= "Организации";	
		СправочникКО 			= "Организации";
	КонецЕсли;	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	КОКонтактнаяИнформация.Представление КАК Представление,
	|	КОКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей
	|ИЗ
	|	Справочник." + СправочникКО + ".КонтактнаяИнформация КАК КОКонтактнаяИнформация
	|ГДЕ
	|	КОКонтактнаяИнформация.Ссылка = &Ссылка
	|	И КОКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдрес" + КонтрагентаОрганизации + ")");
	Запрос.УстановитьПараметр("Ссылка", ОбъектКонтактнойИнформации);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(СтруктураАдреса, Выборка);
		КонецЕсли;	
	КонецЕсли;	
	Возврат(СтруктураАдреса);
КонецФункции

&НаКлиенте
Процедура ВИЛС_ДокументОснованиеПриИзменении(Элемент)
	Если Элемент.Родитель.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Объект.ДокументыОснования.НайтиПоИдентификатору(Элементы.ДокументыОснования.ТекущаяСтрока); 
	
	//НайденныеСтроки = Объект.ДокументыОснования.НайтиСтроки(Новый Структура("ДокументОснование", ТекущаяСтрока.ДокументОснование));             // begin fix Suetin 02.10.2019 14:04:27
	НайденныеСтроки = Объект.ДокументыОснования.НайтиСтроки(Новый Структура("ВИЛС_ДокументОснование", ТекущаяСтрока.ВИЛС_ДокументОснование));          // end fix Suetin 02.10.2019 14:04:31
	
	Если НайденныеСтроки.Количество() > 1 Тогда
		
		Объект.ДокументыОснования.Удалить(НайденныеСтроки[1]);
		Элементы.ДокументыОснования.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
	Если НайденныеСтроки.Количество() Тогда
		НайденныеСтроки[0].ДокументОснование = НайденныеСтроки[0].ВИЛС_ДокументОснование;
	КонецЕсли;	
КонецПроцедуры	

#КонецОбласти

