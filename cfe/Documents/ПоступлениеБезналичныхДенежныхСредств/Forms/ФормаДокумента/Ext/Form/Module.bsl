
&НаСервере
Процедура ВИЛС_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Элементы.РасшифровкаБезРазбиенияЗаказ.УстановитьДействие("ПриИзменении", "ВИЛС_ПриИзмененииРасшифровкаЗаказ_После");
	Элементы.РасшифровкаПлатежаЗаказ.УстановитьДействие("ПриИзменении", "ВИЛС_ПриИзмененииРасшифровкаЗаказ_После");
КонецПроцедуры

&НаСервере
&Вместо("ЗаполнитьДокументВыдачи")
Процедура ВИЛС_ЗаполнитьДокументВыдачи()
	
	Если (Объект.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратНеперечисленныхДС")
		И Объект.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту"))
		Или Не ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Ссылка,
		|	СписаниеБезналичныхДенежныхСредств.Дата КАК Дата
		|ПОМЕСТИТЬ ВТ_Документы
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|ГДЕ
		|	СписаниеБезналичныхДенежныхСредств.Организация = &Организация
		|	И СписаниеБезналичныхДенежныхСредств.ПодотчетноеЛицо = &ПодотчетноеЛицо
		|	И СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента = &БанковскийСчетКонтрагента
		|	И СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета)
		|	И СписаниеБезналичныхДенежныхСредств.БанковскийСчет = &БанковскийСчет
		|	И СписаниеБезналичныхДенежныхСредств.СуммаДокумента = &СуммаДокумента
		|	И СписаниеБезналичныхДенежныхСредств.Дата <= &Дата
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СписаниеБезналичныхДенежныхСредств.Ссылка,
		|	СписаниеБезналичныхДенежныхСредств.Дата
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|ГДЕ
		|	СписаниеБезналичныхДенежныхСредств.Организация = &Организация
		|	И СписаниеБезналичныхДенежныхСредств.Контрагент = &Контрагент
		|	И СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента = &БанковскийСчетКонтрагента
		|	И СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту)
		|	И СписаниеБезналичныхДенежныхСредств.БанковскийСчет = &БанковскийСчет
		|	И СписаниеБезналичныхДенежныхСредств.Дата <= &Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
		|	ВТ_Документы.Ссылка КАК Ссылка,
		|	ВТ_Документы.Дата КАК Дата
		|ИЗ
		|	ВТ_Документы КАК ВТ_Документы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ
		|";
	
	Запрос.УстановитьПараметр("БанковскийСчет",                   Объект.БанковскийСчет);
	Запрос.УстановитьПараметр("БанковскийСчетКонтрагента",        Объект.БанковскийСчетКонтрагента);
	Запрос.УстановитьПараметр("Дата",                             КонецДня(Объект.Дата));
	Запрос.УстановитьПараметр("Организация",                      Объект.Организация);
	Запрос.УстановитьПараметр("ПодотчетноеЛицо",                  Объект.ПодотчетноеЛицо);
	Запрос.УстановитьПараметр("Контрагент",                       Объект.Контрагент);
	Запрос.УстановитьПараметр("СуммаДокумента",                   Объект.СуммаДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Объект.ДокументВыдачи = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
&После("ДополнитьВыбранноеЗначениеДаннымиНДС")
Процедура ВИЛС_ДополнитьВыбранноеЗначениеДаннымиНДС(ВыбранноеЗначение)
	ВИЛС_ЗаполнитьПодразделениеПоОснованию(Объект.Подразделение, ВыбранноеЗначение.ОснованиеПлатежа, ВыбранноеЗначение.Заказ, Объект.Договор);
КонецПроцедуры

&НаКлиенте
Процедура ВИЛС_ПриИзмененииРасшифровкаЗаказ_После(Элемент)
	СтрокаТаблицы = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	ВИЛС_ЗаполнитьПодразделениеПоОснованию(Объект.Подразделение, СтрокаТаблицы.ОснованиеПлатежа, СтрокаТаблицы.Заказ, Объект.Договор);
КонецПроцедуры
 
&НаСервереБезКонтекста                                                            
Процедура ВИЛС_ЗаполнитьПодразделениеПоОснованию(Подразделение, Знач ОснованиеПлатежа, Знач Заказ, Знач Договор)   
	Если ЗначениеЗаполнено(ОснованиеПлатежа)
		и Не ОснованиеПлатежа.Метаданные().Реквизиты.Найти("Подразделение") = Неопределено
			и ЗначениеЗаполнено(ОснованиеПлатежа.Подразделение) Тогда
	    Подразделение = ОснованиеПлатежа.Подразделение;
	ИначеЕсли ЗначениеЗаполнено(Заказ)
		и Не Заказ.Метаданные().Реквизиты.Найти("Подразделение") = Неопределено
			и ЗначениеЗаполнено(Заказ.Подразделение) Тогда
	    Подразделение = Заказ.Подразделение;
	Иначе	
		Подразделение = Договор.Подразделение;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ВИЛС_ДатаПроведенияБанкомПриИзмененииПосле(Элемент)  // begin fix Suetin 21.10.2020 9:34:39  Ticket#8219094
	Если Объект.ПроведеноБанком
		и ЗначениеЗаполнено(Объект.ДатаПроведенияБанком) Тогда
		Объект.Дата = Объект.ДатаПроведенияБанком + (ТекущаяДата() - НачалоДня(ТекущаяДата()));
	КонецЕсли;
КонецПроцедуры                                                 // end fix Suetin 21.10.2020 9:34:45
