#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюПечать

Функция ВывестиНаПечатьДеревоСебестоимостиПродукции(Форма, ДеревоРезультат) Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасчетПлановойСебестоимости";
	Макет = ПолучитьМакет("ПФ_MXL_ДеревоСебестоимостиПродукции_ВИЛС");	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = "Дерево себестоимости продукции";
	ОбластьМакета.Параметры.Дата = Формат(ТекущаяДата(), "ДЛФ=D");
	ПараметрДатаНачала = Форма.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ДатаНачала");
	Если Не ПараметрДатаНачала = Неопределено и ПараметрДатаНачала.Использование Тогда
		ОбластьМакета.Параметры.ДатаНач = Формат(ПараметрДатаНачала.Значение, "ДЛФ=D");	
	КонецЕсли;	
	ПараметрДатаОкончания = Форма.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ДатаОкончания");
	Если Не ПараметрДатаОкончания = Неопределено и ПараметрДатаОкончания.Использование Тогда
		ОбластьМакета.Параметры.ДатаКон = Формат(ПараметрДатаОкончания.Значение, "ДЛФ=D");	
	КонецЕсли;	
	ТабДокумент.Вывести(ОбластьМакета);	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	ТабДокумент.НачатьАвтогруппировкуСтрок();
	
	Для Каждого Строка Из ДеревоРезультат.Строки Цикл
		ВывестиСтрокуРасчетаПлановойСебестоимости(ТабДокумент, Макет, Строка, Валюта);	
	КонецЦикла;
	
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Ответственный = Пользователи.ТекущийПользователь();
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент
КонецФункции // ВывестиНаПечатьДеревоСебестоимостиПродукции

Процедура ВывестиСтрокуРасчетаПлановойСебестоимости(ТабДокумент, Макет, Знач СтрокаДерева, Валюта)
	КоличествоПодчиненныхСтрок = СтрокаДерева.Строки.Количество();
	Если КоличествоПодчиненныхСтрок Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Номенклатура");
	Иначе
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	КонецЕсли;
	ОбластьМакета.Параметры.Заполнить(СтрокаДерева);
	ОбластьМакета.Параметры.Номенклатура = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1%2%3%4%5%6%7%8%9'"), 	СтрокаДерева.Номенклатура,
												?(ЗначениеЗаполнено(СтрокаДерева.Характеристика),  					", ", ""), 
												?(ЗначениеЗаполнено(СтрокаДерева.Характеристика),  					СтрокаДерева.Характеристика, ""),
												?(ЗначениеЗаполнено(СтрокаДерева.Серия),  							", ", ""), 
												?(ЗначениеЗаполнено(СтрокаДерева.Серия),  							СтрокаДерева.Серия, ""),
												?(ЗначениеЗаполнено(СтрокаДерева.Назначение),  						", ", ""), 
												?(ЗначениеЗаполнено(СтрокаДерева.Назначение),  						СтрокаДерева.Назначение, ""),
												?(ЗначениеЗаполнено(СтрокаДерева.Партия),  							", ", ""), 
												?(ЗначениеЗаполнено(СтрокаДерева.Партия),  							СтрокаДерева.Партия, ""));
	
	ОбластьМакета.Параметры.Валюта = Валюта;											
	ОбластьСтрока = ТабДокумент.Вывести(ОбластьМакета, СтрокаДерева.Уровень);
	Если СтрокаДерева.Уровень > 0 Тогда
		ТабДокумент.Область(ОбластьСтрока.Верх,1+1).Отступ = СтрокаДерева.Уровень;
	КонецЕсли;	
	
	Если КоличествоПодчиненныхСтрок Тогда		
		Для Каждого Строка Из СтрокаДерева.Строки Цикл			
			ВывестиСтрокуРасчетаПлановойСебестоимости(ТабДокумент, Макет, Строка, Валюта);			
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ВывестиСтрокуРасчетаПлановойСебестоимости()

#КонецОбласти

#КонецОбласти
		
#КонецЕсли