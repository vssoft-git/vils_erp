
// Функция формирует таблицу партий производства.
//
// Параметры:
//	Параметры - Структура - Структура отборов.
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица партий производства.
//
&ИзменениеИКонтроль("ПартииПроизводстваДляРаспределенияЗатрат")
Функция ВИЛС_ПартииПроизводстваДляРаспределенияЗатрат(Отборы) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
//++ НЕ УТКА
	ПараметрыЗапроса = ОписаниеПараметровПолученияАктивныхПартий();
	ПараметрыЗапроса.НачалоПериода = Отборы.НачалоПериода;
	ПараметрыЗапроса.ОкончаниеПериода = Отборы.ОкончаниеПериода;
	ПараметрыЗапроса.Организации = ОбщегоНазначенияУТКлиентСервер.Массив(Отборы.Организация);
	
	ПоместитьАктивныеПартииБезСпецификаций(ПараметрыЗапроса, МенеджерВременныхТаблиц);
//-- НЕ УТКА
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"
//++ НЕ УТКА
	|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Реквизиты.ПартияПроизводства	КАК ПартияПроизводства,
	|	ИСТИНА							КАК ЕстьВыпускПродукции
	|ПОМЕСТИТЬ ЭтапыСВыпуском
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Изделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
	|			ПО Изделия.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Организация = &Организация
	|	И Реквизиты.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат),
	|						ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен))
	|	И Реквизиты.Проведен
	|	И Изделия.Произведено
	|	И НЕ Изделия.Отменено
	|	И Изделия.ДатаПроизводства МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Реквизиты.ПартияПроизводства	КАК ПартияПроизводства,
	|	ИСТИНА							КАК ЕстьВыпускПродукции
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК Изделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
	|			ПО Изделия.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Организация = &Организация
	|	И Реквизиты.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат),
	|						ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен))
	|	И Реквизиты.Проведен
	|	И Изделия.Произведено
	|	И НЕ Изделия.Отменено
	|	И Изделия.ДатаПроизводства МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	РеквизитыЭтапа.ПартияПроизводства	КАК ПартияПроизводства,
	|	ИСТИНА								КАК ЕстьВыпускПродукции
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК Изделия
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|			ПО РеквизитыЭтапа.Ссылка = Изделия.ЭтапПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК Реквизиты
	|			ПО Изделия.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Организация = &Организация
	|	И Реквизиты.Проведен
	|	И Реквизиты.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Реквизиты.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|
	|;
	|
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Этап,
	|	Реквизиты.Подразделение КАК Подразделение,
	|	Реквизиты.ПартияПроизводства КАК ПартияПроизводства,
	|	Реквизиты.ТипПроизводственногоПроцесса
	|ПОМЕСТИТЬ ЭтапыТекущегоПериода
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Организация = &Организация
	|	И Реквизиты.Проведен
	|	И Реквизиты.ФактическоеНачалоЭтапа <= &ОкончаниеПериода
	|	И (Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат)
	|		ИЛИ Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	|			И Реквизиты.ФактическоеОкончаниеЭтапа >= &НачалоПериода)
	|;
	|
	//-- НЕ УТКА
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпрПартииПроизводства.Ссылка							КАК ПартияПроизводства,
	|	ДанныеДокумента.Подразделение							КАК Подразделение,
	|	ДанныеДокумента.Ссылка									КАК Этап,
	|	СпрПартииПроизводства.ОсновноеИзделиеНоменклатура		КАК Номенклатура,
	|	СпрПартииПроизводства.ОсновноеИзделиеХарактеристика	КАК Характеристика,
	|	СпрПартииПроизводства.Спецификация						КАК Спецификация,
	|	СпрПартииПроизводства.Назначение						КАК Назначение,
	|	СпрПартииПроизводства.НаправлениеДеятельности			КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО											КАК ЗаказНаПроизводство,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ													КАК ЭтапТекущегоМесяца,
	|	ИСТИНА													КАК ЕстьВыпускПродукции,
	|	ЛОЖЬ													КАК ПроизводствоНаСтороне
	|ПОМЕСТИТЬ ВтЭтапы
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаВыходныеИзделия
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаВыходныеИзделия.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаВыходныеИзделия.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаВыходныеИзделия.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|ГДЕ
	|	ДанныеДокумента.Организация = &Организация
	|	И ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(ДобавитьКДате(&ОкончаниеПериода, МЕСЯЦ, 1), МЕСЯЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	СпрПартииПроизводства.Ссылка,
	|	СпрПартииПроизводства.Номенклатура,
	|	СпрПартииПроизводства.Характеристика,
	|	СпрПартииПроизводства.Спецификация,
	|	СпрПартииПроизводства.Назначение,
	|	СпрПартииПроизводства.НаправлениеДеятельности
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АктивныеПартии.ПартияПроизводства 			КАК ПартияПроизводства,
	|	ЕСТЬNULL(Этапы.Подразделение,
	|		ЗаказНаПроизводство.Подразделение) 		КАК Подразделение,
	|	ЕСТЬNULL(Этапы.Этап, 
	|		НЕОПРЕДЕЛЕНО)							КАК Этап,
	|	РеквизитыПП.ОсновноеИзделиеНоменклатура		КАК Номенклатура,
	|	РеквизитыПП.ОсновноеИзделиеХарактеристика	КАК Характеристика,
	|	РеквизитыПП.Спецификация					КАК Спецификация,
	|	РеквизитыПП.Назначение						КАК Назначение,
	|	РеквизитыПП.НаправлениеДеятельности			КАК НаправлениеДеятельности,
	|	ЗаказНаПроизводство.Ссылка 					КАК ЗаказНаПроизводство,
	|	ИСТИНА										КАК ЭтапТекущегоМесяца,
	|	ЛОЖЬ										КАК ЕстьВыпускПродукции,
	|	ЛОЖЬ										КАК ПроизводствоНаСтороне
	|ИЗ
	|	АктивныеПартии КАК АктивныеПартии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство
	|		ПО АктивныеПартии.ПартияПроизводства = ЗаказНаПроизводство.ПартияПроизводства
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК РеквизитыПП
	|		ПО РеквизитыПП.Ссылка = АктивныеПартии.ПартияПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЭтапыТекущегоПериода КАК Этапы
	|		ПО АктивныеПартии.ПартияПроизводства = Этапы.ПартияПроизводства
	|			И Этапы.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.БезСпецификаций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыПП.Ссылка							КАК ПартияПроизводства,
	|	Реквизиты.Подразделение						КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ДетализироватьПоЭтапам
	|			ТОГДА Реквизиты.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ										КАК Этап,
	|	РеквизитыПП.ОсновноеИзделиеНоменклатура		КАК Номенклатура,
	|	РеквизитыПП.ОсновноеИзделиеХарактеристика	КАК Характеристика,
	|	РеквизитыПП.Спецификация					КАК Спецификация,
	|	РеквизитыПП.Назначение						КАК Назначение,
	|	РеквизитыПП.НаправлениеДеятельности			КАК НаправлениеДеятельности,
	|	Реквизиты.Распоряжение						КАК ЗаказНаПроизводство,
	|	ВЫБОР
	|		КОГДА &НачалоПериода МЕЖДУ Реквизиты.ФактическоеНачалоЭтапа И Реквизиты.ФактическоеОкончаниеЭтапа
	|				ИЛИ &НачалоПериода >= Реквизиты.ФактическоеНачалоЭтапа
	|					И Реквизиты.ФактическоеОкончаниеЭтапа = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ &ОкончаниеПериода МЕЖДУ Реквизиты.ФактическоеНачалоЭтапа И Реквизиты.ФактическоеОкончаниеЭтапа
	|				ИЛИ &ОкончаниеПериода >= Реквизиты.ФактическоеНачалоЭтапа
	|					И Реквизиты.ФактическоеОкончаниеЭтапа = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ &НачалоПериода <= Реквизиты.ФактическоеНачалоЭтапа
	|					И &ОкончаниеПериода >= Реквизиты.ФактическоеОкончаниеЭтапа
	|				ИЛИ Реквизиты.ПроизводствоНаСтороне И ЕСТЬNULL(ЭтапыСВыпуском.ЕстьВыпускПродукции, ИСТИНА)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ												КАК ЭтапТекущегоМесяца,
	|	ЕСТЬNULL(ЭтапыСВыпуском.ЕстьВыпускПродукции, ЛОЖЬ)	КАК ЕстьВыпускПродукции,
	|	Реквизиты.ПроизводствоНаСтороне						КАК ПроизводствоНаСтороне
	|ИЗ
	|	ЭтапыТекущегоПериода КАК ЭтапыТекущегоПериода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
	|		ПО ЭтапыТекущегоПериода.Этап = Реквизиты.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК РеквизитыПП
	|		ПО РеквизитыПП.Ссылка = Реквизиты.ПартияПроизводства

	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЭтапыСВыпуском КАК ЭтапыСВыпуском
	|		ПО ЭтапыСВыпуском.ПартияПроизводства = Реквизиты.ПартияПроизводства
	|ГДЕ
	|	НЕ ЭтапыТекущегоПериода.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.БезСпецификаций)
//-- НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпрПартииПроизводства.Ссылка						КАК ПартияПроизводства,
	|	Реквизиты.Подразделение								КАК Подразделение,
	|	Реквизиты.Ссылка									КАК Этап,
	|	СпрПартииПроизводства.ОсновноеИзделиеНоменклатура	КАК Номенклатура,
	|	СпрПартииПроизводства.ОсновноеИзделиеХарактеристика	КАК Характеристика,
	|	СпрПартииПроизводства.Спецификация					КАК Спецификация,
	|	СпрПартииПроизводства.Назначение					КАК Назначение,
	|	СпрПартииПроизводства.НаправлениеДеятельности		КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО										КАК ЗаказНаПроизводство,
	|	ИСТИНА												КАК ЭтапТекущегоМесяца,
	|	ИСТИНА												КАК ЕстьВыпускПродукции,
	|	ИСТИНА												КАК ПроизводствоНаСтороне
	|ИЗ
	|	Документ.ОтчетПереработчика КАК Реквизиты
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|		ПО Реквизиты.Ссылка = ТаблицаТовары.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|ГДЕ
	|	Реквизиты.Организация = &Организация
	|	И Реквизиты.Проведен
	|	И Реквизиты.ГруппировкаЗатрат <> ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	|	И Реквизиты.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.Ссылка,
	|	СпрПартииПроизводства.Ссылка,
	|	СпрПартииПроизводства.Номенклатура,
	|	СпрПартииПроизводства.Характеристика,
	|	СпрПартииПроизводства.Спецификация,
	|	СпрПартииПроизводства.Назначение,
	|	СпрПартииПроизводства.НаправлениеДеятельности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтапТекущегоМесяца,
	|	Подразделение,
	|	Назначение,
	|	НаправлениеДеятельности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтЭтапы.ПартияПроизводства,
	|	ВтЭтапы.Подразделение,
	|	ВтЭтапы.Этап,
	|	ВтЭтапы.Номенклатура,
	|	ВтЭтапы.Характеристика,
	|	ВтЭтапы.Спецификация,
	|	ВтЭтапы.Назначение,
	|	ВтЭтапы.ЗаказНаПроизводство,
	|	ВтЭтапы.ЭтапТекущегоМесяца
#Вставка	
	//begin fix Клещ А.Н. 07.05.2019  
	|	,ВтЭтапы.Этап КАК ВИЛС_ЭтапПроизводства
	//end fix Клещ А.Н. 07.05.2019
#КонецВставки	
	|ИЗ
	|	ВтЭтапы КАК ВтЭтапы
	|ГДЕ
	|	(ВтЭтапы.ЭтапТекущегоМесяца
	|			ИЛИ &ЭтапыЗаВесьПериод)
	|	И (ВтЭтапы.Подразделение = &Подразделение
	|			ИЛИ &ВсеПодразделения)
	|	И (ВтЭтапы.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|			ИЛИ &ВсеЗаказыНаПроизводство)
	|	И (ВтЭтапы.Назначение = &Назначение
	|			ИЛИ &ВсеНазначения)
	|	И (ВтЭтапы.НаправлениеДеятельности = &НаправлениеДеятельности
	|			ИЛИ &ВсеНаправленияДеятельности)
	|	И (ВтЭтапы.ЕстьВыпускПродукции
	|			ИЛИ НЕ &ТолькоЭтапыСВыпуском)
	|	И (НЕ ВтЭтапы.ПроизводствоНаСтороне
	|			ИЛИ НЕ &ИсключатьПроизводствоНаСтороне)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтЭтапы.Характеристика,
	|	ВтЭтапы.ПартияПроизводства,
	|	ВтЭтапы.Этап,
	|	ВтЭтапы.ЭтапТекущегоМесяца,
	|	ВтЭтапы.Спецификация,
	|	ВтЭтапы.Номенклатура,
	|	ВтЭтапы.Назначение,
	|	ВтЭтапы.ЗаказНаПроизводство,
	|	ВтЭтапы.Подразделение";
	
	Подразделение	= Неопределено;
	Назначение		= Неопределено;
	ЗаказНаПроизводство = Неопределено;

	НаправлениеДеятельности = Неопределено;
	ТолькоЭтапыСВыпуском = Отборы.Свойство("ТолькоЭтапыСВыпуском") И Отборы.ТолькоЭтапыСВыпуском;
	ИсключатьПроизводствоНаСтороне = Отборы.Свойство("ИсключатьПроизводствоНаСтороне") И Отборы.ИсключатьПроизводствоНаСтороне;
	
	Отборы.Свойство("Подразделение",           Подразделение);
	Отборы.Свойство("Назначение",              Назначение);

	Отборы.Свойство("ЗаказНаПроизводство",     ЗаказНаПроизводство);
	Отборы.Свойство("НаправлениеДеятельности", НаправлениеДеятельности);
	
	Запрос.УстановитьПараметр("НачалоПериода",                Отборы.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",             Отборы.ОкончаниеПериода);
	
	Запрос.УстановитьПараметр("Организация",                  Отборы.Организация);
	Запрос.УстановитьПараметр("ДетализироватьПоЭтапам",       Отборы.ДетализироватьПоЭтапам);
	Запрос.УстановитьПараметр("ЭтапыЗаВесьПериод",            НЕ Отборы.ТолькоТекущийМесяц);
	
	Запрос.УстановитьПараметр("Подразделение",                Подразделение);
	Запрос.УстановитьПараметр("ВсеПодразделения",             Не ЗначениеЗаполнено(Подразделение));
	
	Запрос.УстановитьПараметр("Назначение",                   Назначение);
	Запрос.УстановитьПараметр("ВсеНазначения",                Не ЗначениеЗаполнено(Назначение));

	Запрос.УстановитьПараметр("ЗаказНаПроизводство",          ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("ВсеЗаказыНаПроизводство",      Не ЗначениеЗаполнено(ЗаказНаПроизводство));
	
	Запрос.УстановитьПараметр("НаправлениеДеятельности",      НаправлениеДеятельности);
	Запрос.УстановитьПараметр("ВсеНаправленияДеятельности",   Не ЗначениеЗаполнено(НаправлениеДеятельности));
	
	Запрос.УстановитьПараметр("ТолькоЭтапыСВыпуском",           ТолькоЭтапыСВыпуском);
	Запрос.УстановитьПараметр("ИсключатьПроизводствоНаСтороне", ИсключатьПроизводствоНаСтороне);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции
