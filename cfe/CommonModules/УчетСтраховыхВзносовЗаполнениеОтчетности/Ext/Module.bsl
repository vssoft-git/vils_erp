
&ИзменениеИКонтроль("РасчетПоказателей_4ФСС_2017Кв1")
Процедура ВИЛС_РасчетПоказателей_4ФСС_2017Кв1(НачалоПериода, ОкончаниеПериода, Организация, ОписаниеТаблиц, ПодробныеДанные)
	
	// ----------------------------------------------------------------------------------
	// ФОРМИРОВАНИЕ ВРЕМЕННЫХ ТАБЛИЦ
	
	// Таблица 1 
	Если ОписаниеТаблиц.Свойство("Доходы2017") Тогда
		
		НачалоТекущегоКвартала = НачалоКвартала(ОкончаниеПериода);
		ЭтоЮридическоеЛицо = ЗарплатаКадры.ЭтоЮридическоеЛицо(Организация);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация",				Организация);
		Запрос.УстановитьПараметр("НачалоОтчетногоПериода",		НачалоПериода);
		Запрос.УстановитьПараметр("КонецОтчетногоПериода",		ОкончаниеПериода);
		Запрос.УстановитьПараметр("Месяц1",						Месяц(НачалоТекущегоКвартала));
		Запрос.УстановитьПараметр("Месяц2",						Месяц(НачалоТекущегоКвартала) + 1);
		Запрос.УстановитьПараметр("Месяц3",						Месяц(НачалоТекущегоКвартала) + 2);
		
		НеОбъект = Новый Массив;
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ДоговорыГПХ);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеОткрытия);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеАудиовизуальныеПроизведения);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеДругиеМузыкальныеПроизведения);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеЛитературныеПроизведения);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеМузыкальноСценическиеПроизведение);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеНаучныеТруды);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеСкульптуры);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеГрафическиеПроизведения);
		НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеИсполнениеПроизведений);
		Если НачалоПериода >= '20190101' Тогда
			НеОбъект.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ДенежноеДовольствиеВоеннослужащих);
		КонецЕсли;
		Запрос.УстановитьПараметр("НеЯвляетсяОбъектом", НеОбъект);
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|ПОМЕСТИТЬ ВТФизЛицаОрганизаций
		|ИЗ
		|	РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СтраховыеВзносыСведенияОДоходах
		|ГДЕ
		|	СтраховыеВзносыСведенияОДоходах.Период МЕЖДУ &НачалоОтчетногоПериода И &КонецОтчетногоПериода
		|	И СтраховыеВзносыСведенияОДоходах.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтраховыеВзносыИсчисленные.ФизическоеЛицо,
		|	СтраховыеВзносыИсчисленные.ГоловнаяОрганизация
		|ИЗ
		|	РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК СтраховыеВзносыИсчисленные
		|ГДЕ
		|	СтраховыеВзносыИсчисленные.Период МЕЖДУ &НачалоОтчетногоПериода И &КонецОтчетногоПериода
		|	И СтраховыеВзносыИсчисленные.Организация = &Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо";
		Запрос.Выполнить();
		
		// Формируем список месяцев, в разрезе которых собираем данные.
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(Запрос.МенеджерВременныхТаблиц, КонецМесяца(НачалоГода(НачалоПериода)), КонецМесяца(ОкончаниеПериода),,, "ВТПериодыРасширенныхСведенийОДоходах", Истина);
		
		// Получаем временные таблицы, используя общие алгоритмы.
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Периоды.Период КАК Период,
		|	СписокСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	СписокСотрудников.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТПериодыФизЛицОрганизаций
		|ИЗ
		|	ВТФизЛицаОрганизаций КАК СписокСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыРасширенныхСведенийОДоходах КАК Периоды
		|		ПО (ИСТИНА)";
		Запрос.Выполнить();
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		ПараметрыПостроения.ВсеЗаписи = Истина;
		
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"ПрименяемыеТарифыСтраховыхВзносов",
			Запрос.МенеджерВременныхТаблиц,
			Ложь,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТПериодыФизЛицОрганизаций",
			"ГоловнаяОрганизация"),
			ПараметрыПостроения);
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		ПараметрыПостроения.ВсеЗаписи = Истина;
		
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"СведенияОбИнвалидностиФизическихЛиц",
			Запрос.МенеджерВременныхТаблиц,
			Ложь,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТПериодыФизЛицОрганизаций",
			"ФизическоеЛицо"),
			ПараметрыПостроения);
		
		Запрос.Текст = 
#Удаление
		"ВЫБРАТЬ
#КонецУдаления
#Вставка
// Здесь можно описать новое поведение.
		"ВЫБРАТЬ первые 0	// fix зимин 21.04.2020
#КонецВставки
		|	НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) КАК ПериодРегистрации,
		|	СписокФизЛиц.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ЕСТЬNULL(ДоходыПоФизлицам.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
		|	СписокФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ЕСТЬNULL(ДанныеОбИнвалидности.Инвалидность, ЛОЖЬ) КАК Инвалид,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДанныеОбИнвалидности.Инвалидность, ЛОЖЬ)
		|			ТОГДА ИСТИНА
		|		КОГДА ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.ВидТарифа = ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.ОрганизацияИнвалидов
		|	КОНЕЦ КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	ЕСТЬNULL(ДоходыПоФизлицам.ВидДохода, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ПустаяСсылка)) КАК ВидДохода,
		|	ЕСТЬNULL(ДоходыПоФизлицам.Начисление, ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка)) КАК Начисление,
		|	ЕСТЬNULL(ДоходыПоФизлицам.ВидДохода.ВходитВБазуФСС_НС, ЛОЖЬ) КАК ВходитВБазуФСС_НС,
		|	ЕСТЬNULL(ДоходыПоФизлицам.Сумма, 0) КАК Сумма,
		|	ЕСТЬNULL(ДоходыПоФизлицам.Скидка, 0) КАК Скидка,
		|	ВЫБОР
		|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА ДоходыПоФизлицам.ВидДохода.ВходитВБазуФСС_НС
		|			ТОГДА ДоходыПоФизлицам.Сумма - ВЫБОР
		|					КОГДА ДоходыПоФизлицам.ВидДохода.АвторскиеВознаграждения
		|						ТОГДА 0
		|					ИНАЧЕ ДоходыПоФизлицам.Скидка
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК БазаФСС_НС,
		|	ЕСТЬNULL(ДоходыПоФизлицам.Регистратор, НЕОПРЕДЕЛЕНО) КАК Регистратор
		|ПОМЕСТИТЬ ВТДанныеУчета
		|ИЗ
		|	ВТПериодыФизЛицОрганизаций КАК СписокФизЛиц
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК ДоходыПоФизлицам
		|		ПО СписокФизЛиц.ГоловнаяОрганизация = ДоходыПоФизлицам.ГоловнаяОрганизация
		|			И СписокФизЛиц.ФизическоеЛицо = ДоходыПоФизлицам.ФизическоеЛицо
		|			И (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ДоходыПоФизлицам.Период, МЕСЯЦ))
		|			И (ДоходыПоФизлицам.Организация = &Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПрименяемыеТарифыСтраховыхВзносовСрезПоследних КАК ПрименяемыеТарифыСтраховыхВзносовСрезПоследних
		|		ПО СписокФизЛиц.ГоловнаяОрганизация = ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.ГоловнаяОрганизация
		|			И (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.Период, МЕСЯЦ))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОбИнвалидностиФизическихЛицСрезПоследних КАК ДанныеОбИнвалидности
		|		ПО (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ДанныеОбИнвалидности.Период, МЕСЯЦ))
		|			И СписокФизЛиц.ФизическоеЛицо = ДанныеОбИнвалидности.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СведенияОДоходах.ПериодРегистрации КАК ПериодРегистрации,
		|	СведенияОДоходах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.Начисление КАК Начисление,
		|	СведенияОДоходах.ВходитВБазуФСС_НС КАК ВходитВБазуФСС_НС,
		|	СУММА(СведенияОДоходах.Сумма) КАК Сумма,
		|	СУММА(СведенияОДоходах.Скидка) КАК Скидка,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
		|	СведенияОДоходах.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТСведенияОДоходах
		|ИЗ
		|	ВТДанныеУчета КАК СведенияОДоходах
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ПериодРегистрации,
		|	СведенияОДоходах.ГоловнаяОрганизация,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.Инвалид,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	СведенияОДоходах.ВидДохода,
		|	СведенияОДоходах.Начисление,
		|	СведенияОДоходах.ВходитВБазуФСС_НС,
		|	СведенияОДоходах.Регистратор
		|
		|ИМЕЮЩИЕ
		|	(СУММА(СведенияОДоходах.Сумма) <> 0
		|		ИЛИ СУММА(СведенияОДоходах.Скидка) <> 0
		|		ИЛИ СУММА(СведенияОДоходах.БазаФСС_НС) <> 0)";
		Запрос.Выполнить();
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОДоходах.ПериодРегистрации КАК ПериодРегистрации
		|ПОМЕСТИТЬ ВТМесяцыБезОблагаемойБазы
		|ИЗ
		|	ВТСведенияОДоходах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоОтчетногоПериода И &КонецОтчетногоПериода
		|	И СведенияОДоходах.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ПериодРегистрации
		|
		|ИМЕЮЩИЕ
		|	СУММА(СведенияОДоходах.БазаФСС_НС) < 0";
		Результат = Запрос.Выполнить();
		
		Если Результат.Выгрузить()[0].Количество = 0 Тогда
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
			|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодРегистрации
			|ПОМЕСТИТЬ ВТФизлицаБезОблагаемойБазы";
		Иначе
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СведенияОДоходах.ПериодРегистрации КАК ПериодРегистрации
			|ПОМЕСТИТЬ ВТФизлицаБезОблагаемойБазы
			|ИЗ
			|	ВТСведенияОДоходах КАК СведенияОДоходах
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыБезОблагаемойБазы КАК МесяцыБезОблагаемойБазы
			|		ПО СведенияОДоходах.ПериодРегистрации = МесяцыБезОблагаемойБазы.ПериодРегистрации
			|ГДЕ
			|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоОтчетногоПериода И &КонецОтчетногоПериода
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОДоходах.ФизическоеЛицо,
			|	СведенияОДоходах.ПериодРегистрации
			|
			|ИМЕЮЩИЕ
			|	СУММА(СведенияОДоходах.БазаФСС_НС) < 0
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ФизическоеЛицо";
		КонецЕсли;
		Запрос.Выполнить();
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОДоходах.ПериодРегистрации КАК Период,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Начисление КАК Начисление,
		|	СведенияОДоходах.Регистратор КАК Регистратор,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода В (&НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ВходитВБазуФСС_НС
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.Скидка
		|		КОНЕЦ) КАК НеОблагаетсяНС_ПЗ,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаНС_ПЗ,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС
		|				ТОГДА СведенияОДоходах.БазаФСС_НС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаИнвалидаНС_ПЗ
		|ПОМЕСТИТЬ ВТОблагаемыеБазыПоМесяцам
		|ИЗ
		|	ВТСведенияОДоходах КАК СведенияОДоходах
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФизлицаБезОблагаемойБазы КАК ФизлицаБезОблагаемойБазы
		|		ПО СведенияОДоходах.ФизическоеЛицо = ФизлицаБезОблагаемойБазы.ФизическоеЛицо
		|			И СведенияОДоходах.ПериодРегистрации = ФизлицаБезОблагаемойБазы.ПериодРегистрации
		|ГДЕ
		|	СведенияОДоходах.Организация = &Организация
		|	И СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоОтчетногоПериода И &КонецОтчетногоПериода
		|	И ФизлицаБезОблагаемойБазы.ФизическоеЛицо ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.Регистратор,
		|	СведенияОДоходах.ПериодРегистрации,
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.Начисление";
		Запрос.Выполнить();
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(СведенияОДоходах.БазаНС_ПЗ + СведенияОДоходах.НеОблагаетсяНС_ПЗ), 0) КАК П000010000103,
		|	ЕСТЬNULL(СУММА(СведенияОДоходах.БазаИнвалидаНС_ПЗ), 0) КАК П000010000403,
		|	ЕСТЬNULL(СУММА(СведенияОДоходах.НеОблагаетсяНС_ПЗ), 0) КАК П000010000203,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц1
		|					ТОГДА СведенияОДоходах.БазаНС_ПЗ + СведенияОДоходах.НеОблагаетсяНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000104,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц2
		|					ТОГДА СведенияОДоходах.БазаНС_ПЗ + СведенияОДоходах.НеОблагаетсяНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000105,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц3
		|					ТОГДА СведенияОДоходах.БазаНС_ПЗ + СведенияОДоходах.НеОблагаетсяНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000106,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц1
		|					ТОГДА СведенияОДоходах.БазаИнвалидаНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000404,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц2
		|					ТОГДА СведенияОДоходах.БазаИнвалидаНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000405,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц3
		|					ТОГДА СведенияОДоходах.БазаИнвалидаНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000406,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц1
		|					ТОГДА СведенияОДоходах.НеОблагаетсяНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000204,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц2
		|					ТОГДА СведенияОДоходах.НеОблагаетсяНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000205,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА МЕСЯЦ(СведенияОДоходах.Период) = &Месяц3
		|					ТОГДА СведенияОДоходах.НеОблагаетсяНС_ПЗ
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК П000010000206,
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.Начисление,
		|	СведенияОДоходах.Регистратор
		|ИЗ
		|	ВТОблагаемыеБазыПоМесяцам КАК СведенияОДоходах
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.Начисление,
		|	СведенияОДоходах.Регистратор";
		
		МассивПолей = Новый Массив;
		Если Не ПодробныеДанные Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СведенияОДоходах.ФизическоеЛицо,", "");
			МассивПолей.Добавить("Начисление");
			МассивПолей.Добавить("Регистратор");
		КонецЕсли;
		Запрос.Текст = УдалитьПоляРасшифровкиИзЗапроса(ТекстЗапроса, МассивПолей);
		ОписаниеТаблиц.Доходы2017 = Запрос.Выполнить().Выгрузить()
		
	КонецЕсли;
	
КонецПроцедуры
