
&После("ПриСозданииНаСервере")
Процедура ВИЛС_ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка)
	Если Форма.ИмяФормы	= "Документ.СчетФактураВыданныйАванс.Форма.ФормаРабочееМесто" Тогда
		// begin fix Suetin 20.02.2019 18:45:45
		//Реквизиты
		МассивДобавляемыхРеквизитов = Новый Массив;
		Реквизит = Новый РеквизитФормы("ВИЛС_Руководитель",Новый ОписаниеТипов("СправочникСсылка.ОтветственныеЛицаОрганизаций"),,"Руководитель");
		МассивДобавляемыхРеквизитов.Добавить(Реквизит);
		Реквизит = Новый РеквизитФормы("ВИЛС_ГлавныйБухгалтер",Новый ОписаниеТипов("СправочникСсылка.ОтветственныеЛицаОрганизаций"),,"Главный бухгалтер");
		МассивДобавляемыхРеквизитов.Добавить(Реквизит);
		
		Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);		
		
		//Элементы
		ЭлементГр = Форма.Элементы.Вставить("ГруппаОтветственныеЛица", Тип("ГруппаФормы"), Форма.Элементы.ФормированиеСчетовФактурНаАванс,Форма.Элементы.ФормированиеСчетовФактурНаАванс.ПодчиненныеЭлементы.ГруппаПолученныеАвансы);
		ЭлементГр.Вид 					= ВидГруппыФормы.ОбычнаяГруппа;
		ЭлементГр.Отображение 			= ОтображениеОбычнойГруппы.Нет;
		ЭлементГр.ОтображатьЗаголовок 	= Ложь;
		
		МассивПВ = Новый Массив();
		СтуктураПВ = Новый ПараметрВыбора("Отбор.ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		МассивПВ.Добавить(СтуктураПВ);
		
		МассивСПВ = Новый Массив();
		СтуктураСПВ = Новый СвязьПараметраВыбора("Отбор.Дата", "КонецПериода");
		МассивСПВ.Добавить(СтуктураСПВ); 
		СтуктураСПВ = Новый СвязьПараметраВыбора("Отбор.Владелец", "Организация");
		МассивСПВ.Добавить(СтуктураСПВ);
		
		НовыйЭлемент = Форма.Элементы.Вставить("ВИЛС_Руководитель",Тип("ПолеФормы"), ЭлементГр);
		НовыйЭлемент.ПутьКДанным = "ВИЛС_Руководитель";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Ширина = 30;
		НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
		НовыйЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПВ);
		НовыйЭлемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСПВ);
		
		МассивПВ = Новый Массив();
		СтуктураПВ = Новый ПараметрВыбора("Отбор.ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
		МассивПВ.Добавить(СтуктураПВ);

		НовыйЭлемент = Форма.Элементы.Вставить("ВИЛС_ГлавныйБухгалтер",Тип("ПолеФормы"), ЭлементГр);
		НовыйЭлемент.ПутьКДанным = "ВИЛС_ГлавныйБухгалтер";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Ширина = 30;
		НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
		НовыйЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПВ);
		НовыйЭлемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСПВ);
		// end fix Suetin 20.02.2019 18:45:49
	ИначеЕсли Форма.ИмяФормы = "Документ.ДвижениеПродукцииИМатериалов.Форма.ФормаДокумента" Тогда
		// begin fix Suetin 03.06.2020 0:52:44
		Если Форма.Элементы.Найти("ВИЛС_ЗаказКлиента") = Неопределено Тогда
			ДобавляемыйЭлемент = Форма.Элементы.Вставить("ВИЛС_ЗаказКлиента", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво); 													 
			ДобавляемыйЭлемент.Вид 								= ВидПоляФормы.ПолеВвода; 
			ДобавляемыйЭлемент.ПутьКДанным 						= "Объект.ВИЛС_ЗаказКлиента";
			ЗаполнитьЗначенияСвойств(ДобавляемыйЭлемент, Форма.Элементы.ГруппаШапкаЛево.ПодчиненныеЭлементы.Получатель,, "ВыделенныйТекст,Заголовок,ОграничениеТипа,ПараметрыВыбора,Подсказка,ПодсказкаВвода,ПутьКДанным,ПутьКДаннымПодвала,РасширеннаяПодсказка,СвязиПараметровВыбора,СвязьПоТипу,СочетаниеКлавиш,СписокВыбора");
			ДобавляемыйЭлемент.КнопкаВыбора                     = Истина;
			ДобавляемыйЭлемент.КнопкаВыпадающегоСписка 			= Ложь;
			ДобавляемыйЭлемент.Заголовок 						= "Заказ клиента";
		КонецЕсли;
	    // end fix Suetin 03.06.2020 0:52:53
	КонецЕсли; 
КонецПроцедуры
