
&ИзменениеИКонтроль("ОрганизацииСВыпускомПродукцииБезФИФОСкользящая")
Функция ВИЛС_ОрганизацииСВыпускомПродукцииБезФИФОСкользящая(НачалоПериода, КонецПериода, КомпоновщикНастроек, ИмяПоляОрганизация)
	
	МассивОрганизаций = ОрганизацииДляОтбораИзНастроекОтчета(КомпоновщикНастроек, ИмяПоляОрганизация);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	РегистрыСведений.УчетнаяПолитикаОрганизаций.СформироватьВТУчетныеПолитикиОрганизаций(Запрос, , НачалоПериода);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыпускПродукции.Организация КАК Ссылка
	|ПОМЕСТИТЬ ВТОрганизацииСДвижениями
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции.Обороты(&НачалоПериода, &КонецПериода, , Организация В (&МассивОрганизаций)) КАК ВыпускПродукции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСДвижениями КАК ОрганизацииСДвижениями
	|		ПО Т.Ссылка = ОрганизацииСДвижениями.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУчетныеПолитикиОрганизаций КАК УчетныеПолитикиОрганизаций
	|		ПО Т.Ссылка = УчетныеПолитикиОрганизаций.Организация
	|ГДЕ
	|	УчетныеПолитикиОрганизаций.МетодОценкиСтоимостиТоваров <> ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка)
	|	И УчетныеПолитикиОрганизаций.УчетнаяПолитикаСуществует
#Вставка	
	|	И НЕ Т.Ссылка = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
#КонецВставки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";

	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивИсключенныхОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивИсключенныхОрганизаций;
	
КонецФункции
