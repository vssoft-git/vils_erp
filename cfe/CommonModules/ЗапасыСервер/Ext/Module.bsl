	
&ИзменениеИКонтроль("ПоместитьТоварыИВидыЗапасовВХранилище")
Функция ВИЛС_ПоместитьТоварыИВидыЗапасовВХранилище(Форма, ПараметрыРедактированияВидовЗапасов)
	
	Если ПараметрыРедактированияВидовЗапасов = Неопределено Тогда
		ПараметрыРедактированияВидовЗапасов = ПараметрыРедактированияВидовЗапасов();
	КонецЕсли;
	
	Товары                  = Форма.Объект[ПараметрыРедактированияВидовЗапасов.ИмяТЧТовары];
	ВидыЗапасов             = Форма.Объект[ПараметрыРедактированияВидовЗапасов.ИмяТЧВидыЗапасов];
	УникальныйИдентификатор = Форма.УникальныйИдентификатор;
		
	Если ПараметрыРедактированияВидовЗапасов.ТоварВШапке Тогда
		
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
		ТаблицаТоваров.Колонки.Добавить("АналитикаУчетаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
		ТаблицаТоваров.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТоваров.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаТоваров.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Форма.Объект);
		НоваяСтрока.НомерСтроки = 1;
		
	Иначе
		
		ТаблицаТоваров = Товары.Выгрузить();
		
		Если ПараметрыРедактированияВидовЗапасов.ИмяКолонкиНоменклатура <> "Номенклатура" Тогда
			Если ТаблицаТоваров.Колонки.Найти("Номенклатура") <> Неопределено Тогда
				ТаблицаТоваров.Колонки.Удалить("Номенклатура");
			КонецЕсли;
			
			ТаблицаТоваров.Колонки[ПараметрыРедактированияВидовЗапасов.ИмяКолонкиНоменклатура].Имя = "Номенклатура";
		КонецЕсли;
		
		Если ПараметрыРедактированияВидовЗапасов.ИмяКолонкиХарактеристика <> "Характеристика" Тогда
			Если ТаблицаТоваров.Колонки.Найти("Характеристика") <> Неопределено Тогда
				ТаблицаТоваров.Колонки.Удалить("Характеристика");
			КонецЕсли;
			
			ТаблицаТоваров.Колонки[ПараметрыРедактированияВидовЗапасов.ИмяКолонкиХарактеристика].Имя = "Характеристика";
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыРедактированияВидовЗапасов.АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(
		ТаблицаТоваров,
		УникальныйИдентификатор);
	
	ТаблицаВидовЗапасов = ВидыЗапасов.Выгрузить();
	
	ПараметрыРедактированияВидовЗапасов.АдресВидовЗапасовВХранилище = ПоместитьВоВременноеХранилище(
		ТаблицаВидовЗапасов,
		УникальныйИдентификатор);
		
	Если Не ЗначениеЗаполнено(ПараметрыРедактированияВидовЗапасов.Организация) Тогда
		ПараметрыРедактированияВидовЗапасов.Организация = Форма.Объект.Организация;
	КонецЕсли;
	
#Удаление
	ПараметрыРедактированияВидовЗапасов.ВидыЗапасовУказаныВручную = Форма.Объект.ВидыЗапасовУказаныВручную;
	
#КонецУдаления
#Вставка
	// begin fix Suetin 27.08.2020 17:26:57 Иачее ошибка
	Если Форма.Объект.Свойство("ВидыЗапасовУказаныВручную") Тогда
		ПараметрыРедактированияВидовЗапасов.ВидыЗапасовУказаныВручную = Форма.Объект.ВидыЗапасовУказаныВручную;
	Иначе
		ПараметрыРедактированияВидовЗапасов.ВидыЗапасовУказаныВручную = Ложь;
	КонецЕсли;
	// end fix Suetin 27.08.2020 17:27:15
#КонецВставки
	Если Форма.Объект.Свойство("ЦенаВключаетНДС") Тогда
		ПараметрыРедактированияВидовЗапасов.ЦенаВключаетНДС = Форма.Объект.ЦенаВключаетНДС;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыРедактированияВидовЗапасов.Склад)
		И Форма.Объект.Свойство("Склад") Тогда
		ПараметрыРедактированияВидовЗапасов.Склад = Форма.Объект.Склад;
	КонецЕсли;
	
	ПараметрыРедактированияВидовЗапасов.ДокументМодифицирован = Форма.Модифицированность;
	
	ПараметрыРедактированияВидовЗапасов.РедактироватьВидыЗапасов =
		ПараметрыРедактированияВидовЗапасов.РедактироватьВидыЗапасов
		И Не Форма.ТолькоПросмотр
		И Форма.Доступность
		И ПраваПользователяПовтИсп.РедактированиеВидовЗапасовДокументов();
		
	Если Форма.Объект.Свойство("Дата") Тогда
		ПараметрыРедактированияВидовЗапасов.ДатаДокумента = Форма.Объект.Дата;
	КонецЕсли;
	
	Возврат ПараметрыРедактированияВидовЗапасов;
	
КонецФункции
 