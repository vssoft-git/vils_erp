
&ИзменениеИКонтроль("ИменаСправочниковХраненияФайлов")
Функция ВИЛС_ИменаСправочниковХраненияФайлов(ВладелецФайлов, НеВызыватьИсключение)

	Если ТипЗнч(ВладелецФайлов) = Тип("Тип") Тогда
		ТипВладельцаФайлов = ВладелецФайлов;
	Иначе
		ТипВладельцаФайлов = ТипЗнч(ВладелецФайлов);
	КонецЕсли;

	МетаданныеВладельца = Метаданные.НайтиПоТипу(ТипВладельцаФайлов);

	ИменаСправочников = Новый Соответствие;

	Если МетаданныеВладельца <> Неопределено Тогда
		ИмяСтандартногоОсновногоСправочника = МетаданныеВладельца.Имя
		+ ?(СтрЗаканчиваетсяНа(МетаданныеВладельца.Имя, "ПрисоединенныеФайлы"), "", "ПрисоединенныеФайлы");

		Если Метаданные.Справочники.Найти(ИмяСтандартногоОсновногоСправочника) <> Неопределено Тогда
			ИменаСправочников.Вставить(ИмяСтандартногоОсновногоСправочника, Истина);
		ИначеЕсли Метаданные.ОпределяемыеТипы.ВладелецФайлов.Тип.СодержитТип(ТипВладельцаФайлов) Тогда
			ИменаСправочников.Вставить("Файлы", Истина);
#Вставка
		ИначеЕсли Метаданные.Справочники.Найти(СтрШаблон("Удалить%1",ИмяСтандартногоОсновногоСправочника)) <> Неопределено Тогда
			ИменаСправочников.Вставить(СтрШаблон("Удалить%1",ИмяСтандартногоОсновногоСправочника), Истина);
#КонецВставки
		КонецЕсли;

		// Переопределение стандартного справочника хранения присоединенных файлов.
		РаботаСФайламиПереопределяемый.ПриОпределенииСправочниковХраненияФайлов(
		ТипВладельцаФайлов, ИменаСправочников);
	КонецЕсли;

	ОсновнойСправочникУказан = Ложь;

	Для каждого КлючИЗначение Из ИменаСправочников Цикл

		Если Метаданные.Справочники.Найти(КлючИЗначение.Ключ) = Неопределено Тогда

			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при определении имен справочников для хранения файлов.
			|У владельца файлов типа ""%1""
			|указан несуществующий справочник ""%2"".';
			|en = 'Cannot determine a file storage catalog name.
			|Missing catalog ""%2""
			|is assigned to the owner of files of ""%1"" type.'"),
			Строка(ТипВладельцаФайлов),
			Строка(КлючИЗначение.Ключ));

		ИначеЕсли Не СтрЗаканчиваетсяНа(КлючИЗначение.Ключ, "ПрисоединенныеФайлы") И Не КлючИЗначение.Ключ ="Файлы" Тогда

			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при определении имен справочников для хранения файлов.
			|У владельца файлов типа ""%1""
			|указано имя справочника ""%2""
			|без окончания ""ПрисоединенныеФайлы"".';
			|en = 'Cannot determine a file storage catalog name.
			|Catalog ""%2""
			|that does not have the ""ПрисоединенныеФайлы"" suffix
			|is assigned to the file owner of ""%1"" type.'"),
			Строка(ТипВладельцаФайлов),
			Строка(КлючИЗначение.Ключ));

		ИначеЕсли КлючИЗначение.Значение = Неопределено Тогда
			ИменаСправочников.Вставить(КлючИЗначение.Ключ, Ложь);

		ИначеЕсли КлючИЗначение.Значение = Истина Тогда
			Если ОсновнойСправочникУказан Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при определении имен справочников для хранения файлов.
				|У владельца файлов типа ""%1""
				|основной справочник указан более одного раза.';
				|en = 'Cannot determine a file storage catalog name.
				|Multiple main catalogs are assigned to the owner of files of 
				|""%1"" type.'"),
				Строка(ТипВладельцаФайлов),
				Строка(КлючИЗначение.Ключ));
			КонецЕсли;
			ОсновнойСправочникУказан = Истина;
		КонецЕсли;
	КонецЦикла;

	Если ИменаСправочников.Количество() = 0 Тогда

		Если НеВызыватьИсключение Тогда
			Возврат ИменаСправочников;
		КонецЕсли;

		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Ошибка при определении имен справочников для хранения файлов.
		|У владельца файлов типа ""%1""
		|не имеется справочников для хранения файлов.';
		|en = 'Cannot determine a file storage catalog name.
		|The owner of files of ""%1"" type
		|does not have file storage catalogs.'"),
		Строка(ТипВладельцаФайлов));
	КонецЕсли;

	Возврат ИменаСправочников;

КонецФункции
