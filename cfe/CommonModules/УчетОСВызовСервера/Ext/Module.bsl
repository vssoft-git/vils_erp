
&ИзменениеИКонтроль("ДобавитьСтрокиВТаблицуАмортизации")
Процедура ВИЛС_ДобавитьСтрокиВТаблицуАмортизации(Таблица, ВыборкаДанных, НовыеСтроки, ТаблицаРаспределения)

	Для Каждого Строка Из НовыеСтроки Цикл

		Если (Строка.СуммаБУ = 0 И Строка.СуммаНУ=0 И Строка.СуммаПР=0 И Строка.СуммаВР=0) Тогда
			Продолжить;
		КонецЕсли;

		Если ТаблицаРаспределения = Неопределено Тогда
			СтрокаТаблицыЗатрат = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, ВыборкаДанных);
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, Строка);

			Если Строка.ЭтоАмортизационнаяПремия Тогда
				СтрокаТаблицыЗатрат.СтатьяРасходов = ВыборкаДанных.СтатьяРасходовАмортизационнойПремии;
				СтрокаТаблицыЗатрат.АналитикаРасходов = ВыборкаДанных.АналитикаРасходовАмортизационнойПремии;
			КонецЕсли;
		Иначе
			МассивКоэффициентов = ТаблицаРаспределения.ВыгрузитьКолонку("Коэффициент");
			СуммыБУ = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаБУ, МассивКоэффициентов);
			СуммыНУ = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаНУ, МассивКоэффициентов);
			СуммыПР = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаПР, МассивКоэффициентов);
			СуммыВР = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаВР, МассивКоэффициентов);
#Удаление
			Коэффициенты = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.Коэффициент, МассивКоэффициентов, 36);

#КонецУдаления
#Вставка
			Коэффициенты = ОбщегоНазначения.ВИЛС_РаспределитьСуммуПропорциональноКоэффициентам(Строка.Коэффициент, МассивКоэффициентов, 36);

#КонецВставки
			Если СуммыБУ = Неопределено И СуммыНУ = Неопределено И СуммыПР = Неопределено И СуммыВР = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			Для Индекс=0 По ТаблицаРаспределения.Количество()-1 Цикл
				ПравилоОтражения = ТаблицаРаспределения[Индекс];
				СтрокаТаблицыЗатрат = Таблица.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, ВыборкаДанных);
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, Строка);
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, ПравилоОтражения);
				Если Строка.ЭтоАмортизационнаяПремия Тогда
					СтрокаТаблицыЗатрат.СтатьяРасходов = ПравилоОтражения.СтатьяРасходовАмортизационнойПремии;
					СтрокаТаблицыЗатрат.АналитикаРасходов = ПравилоОтражения.АналитикаРасходовАмортизационнойПремии;
				КонецЕсли;
				СтрокаТаблицыЗатрат.СуммаБУ = ?(СуммыБУ=Неопределено, 0, СуммыБУ[Индекс]);
				СтрокаТаблицыЗатрат.СуммаНУ = ?(СуммыНУ=Неопределено, 0, СуммыНУ[Индекс]);
				СтрокаТаблицыЗатрат.СуммаПР = ?(СуммыПР=Неопределено, 0, СуммыПР[Индекс]);
				СтрокаТаблицыЗатрат.СуммаВР = ?(СуммыВР=Неопределено, 0, СуммыВР[Индекс]);
				СтрокаТаблицыЗатрат.Коэффициент = ?(Коэффициенты=Неопределено, 0, Коэффициенты[Индекс]);

			КонецЦикла;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры
