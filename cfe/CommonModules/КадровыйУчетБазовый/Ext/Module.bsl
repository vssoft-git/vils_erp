
&ИзменениеИКонтроль("ЗапросВТСведенияОКлассахУсловийТруда")
Функция ВИЛС_ЗапросВТСведенияОКлассахУсловийТруда(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИмяВТОтборовДолжностей, ИмяПоляДолжность, ИсточникиДанных, ИмяВТСведенияОКлассахУсловийТруда = "ВТСведенияОКлассахУсловийТруда") Экспорт
	
	Если Не ЗначениеЗаполнено(ИмяВТОтборовДолжностей) Тогда
		
		УничтожитьВТОтборовДолжностей = Истина;
		
#Удаление
		ТекстЗапросаТекущихДанных = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаОтборов.Период КАК Период,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность КАК Должность
			|ПОМЕСТИТЬ ВТОтборовДолжностейДляКлассовУсловийТруда
			|ИЗ
			|	ВТОтборовСотрудников КАК ТаблицаОтборов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО ТаблицаОтборов.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник";
#КонецУдаления
#Вставка
		//begin fix Клещ А.Н. 28.06.2019
		Организация = Справочники.Организации.НайтиПоНаименованию("ОАО ""ВИЛС""");
		Если ВИЛС_Служебный.ИспользуютсяОсобыеУсловияТруда(Организация) Тогда
			ТекстЗапросаТекущихДанных = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаОтборов.Период КАК Период,
			|	ТаблицаОтборов.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ВТОтборовДолжностейДляКлассовУсловийТруда
			|ИЗ
			|	ВТОтборовСотрудников КАК ТаблицаОтборов
			|";
		Иначе
			ТекстЗапросаТекущихДанных = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаОтборов.Период КАК Период,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность КАК Должность
			|ПОМЕСТИТЬ ВТОтборовДолжностейДляКлассовУсловийТруда
			|ИЗ
			|	ВТОтборовСотрудников КАК ТаблицаОтборов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО ТаблицаОтборов.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник";
		КонецЕсли;
		//end fix Клещ А.Н. 28.06.2019
#КонецВставки
		
		ТекстЗапросаТекущихДанных = СтрЗаменить(ТекстЗапросаТекущихДанных, "ВТОтборовСотрудников",
			ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовСотрудников);
		
		ТекстЗапросаТекущихДанных = СтрЗаменить(ТекстЗапросаТекущихДанных, "ТаблицаОтборов.Период",
			"ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
		
		ТекстЗапросаТекущихДанных = СтрЗаменить(ТекстЗапросаТекущихДанных, "ТаблицаОтборов.Сотрудник",
			"ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник);
		
#Удаление
		ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТОтборовДолжностейДляКлассовУсловийТруда", "Должность");
#КонецУдаления
#Вставка
		//begin fix Клещ А.Н. 14.08.2019  
		Если ВИЛС_Служебный.ИспользуютсяОсобыеУсловияТруда(Организация) Тогда
			ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТОтборовДолжностейДляКлассовУсловийТруда", "Сотрудник");
		Иначе
			ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТОтборовДолжностейДляКлассовУсловийТруда", "Должность");
		КонецЕсли;
		//end fix Клещ А.Н. 14.08.2019
#КонецВставки
		
	Иначе
		
		УничтожитьВТОтборовДолжностей = Ложь;
		ТекстЗапросаТекущихДанных = "";
#Удаление
		ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИмяВТОтборовДолжностей, "Должность");
		ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Должность", ИмяПоляДолжность);
#КонецУдаления
#Вставка
		//begin fix Клещ А.Н. 14.08.2019  
		Организация = Справочники.Организации.НайтиПоНаименованию("ОАО ""ВИЛС""");
		Если ВИЛС_Служебный.ИспользуютсяОсобыеУсловияТруда(Организация) Тогда
			ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИмяВТОтборовДолжностей, "Сотрудник");
			ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Сотрудник", ИмяПоляДолжность);
		Иначе
			ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИмяВТОтборовДолжностей, "Должность");
			ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Должность", ИмяПоляДолжность);
		КонецЕсли;
		//end fix Клещ А.Н. 14.08.2019
#КонецВставки
		
	КонецЕсли;
	
#Удаление
	Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"КлассыУсловийТрудаПоДолжностям",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		,
		Истина,
		ИмяВТСведенияОКлассахУсловийТруда);
#КонецУдаления
#Вставка
	//begin fix Клещ А.Н. 14.08.2019  
	Организация = Справочники.Организации.НайтиПоНаименованию("ОАО ""ВИЛС""");
	Если ВИЛС_Служебный.ИспользуютсяОсобыеУсловияТруда(Организация) Тогда
		Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ВИЛС_ДанныеУсловийТрудаПоСотрудникам",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		,
		Истина,
		ИмяВТСведенияОКлассахУсловийТруда);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"УсловияТруда,","КлассУсловийТруда,");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДолжностьПоШтатномуРасписанию","Сотрудник");
		
		Если не ИсточникиДанных.Получить("СведенийОКлассахУсловийТруда") = Неопределено Тогда
			ИсточникиДанных.Вставить("СведенийОКлассахУсловийТруда","Сотрудник");
		КонецЕсли;
	Иначе
		Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"КлассыУсловийТрудаПоДолжностям",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		,
		Истина,
		ИмяВТСведенияОКлассахУсловийТруда);
	КонецЕсли;
	//end fix Клещ А.Н. 14.08.2019
#КонецВставки
	
	Если Не ПустаяСтрока(ТекстЗапросаТекущихДанных) Тогда
		
		ТекстыЗапросов = Новый Массив;
		ТекстыЗапросов.Добавить(ТекстЗапросаТекущихДанных);
		
		Если УничтожитьВТОтборовДолжностей Тогда
			КадровыйУчет.ДобавитьВКоллекциюИмяКадровыхДанных(ИсточникиДанных, "ВТКУничтожению", "ВТОтборовДолжностейДляКлассовУсловийТруда");
		КонецЕсли;
		
		ТекстыЗапросов.Добавить(Запрос.Текст);
		
		Запрос.Текст = СтрСоединить(ТекстыЗапросов, ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов());
		
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

&ИзменениеИКонтроль("ДобавитьТекстЗапросаВТСведенияОКлассахУсловийТруда")
Процедура ВИЛС_ДобавитьТекстЗапросаВТСведенияОКлассахУсловийТруда(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных)
	
	Если ИсточникиДанных.Получить("СведенийОКлассахУсловийТруда") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоляДолжность = ИсточникиДанных.Получить("СведенийОКлассахУсловийТруда");
	
	ЗапросВТ = ЗапросВТСведенияОКлассахУсловийТруда(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов,
		ИсточникиДанных.Получить("ИмяВТСведенияКадровойИсторииСотрудников"), ИмяПоляДолжность, ИсточникиДанных);
	
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	ТаблицыЗапроса = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Запрос.Текст, ";", , Истина);
	ТекстПоследнегоЗапроса = ТаблицыЗапроса[ТаблицыЗапроса.Количество() - 1];
	
	ТекстЗаменыПоследнегоЗапроса = 
		ЗапросВТ.Текст
		+ Символы.ПС + ";" + Символы.ПС
		+ ТекстПоследнегоЗапроса;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, ТекстПоследнегоЗапроса, ТекстЗаменыПоследнегоЗапроса);
	
	ЧастиЗапроса = Новый Массив;
	ЧастиЗапроса.Добавить(Запрос.Текст);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
#Удаление
		ЧастиЗапроса.Добавить(
			"		{ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОКлассахУсловийТруда КАК КлассыУсловийТрудаПоДолжностям
			|		ПО КадроваяИсторияСотрудников." + ИмяПоляДолжность + " = КлассыУсловийТрудаПоДолжностям.Должность
			|			И ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = КлассыУсловийТрудаПоДолжностям.Период}");
#КонецУдаления
#Вставка
		//begin fix Клещ А.Н. 14.08.2019  
		Организация = Справочники.Организации.НайтиПоНаименованию("ОАО ""ВИЛС""");
		Если ВИЛС_Служебный.ИспользуютсяОсобыеУсловияТруда(Организация) Тогда
					ЧастиЗапроса.Добавить(
				"		{ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОКлассахУсловийТруда КАК КлассыУсловийТрудаПоДолжностям
				|		ПО КадроваяИсторияСотрудников." + "Сотрудник" + " = КлассыУсловийТрудаПоДолжностям.Сотрудник
				|			И ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = КлассыУсловийТрудаПоДолжностям.Период}");

		Иначе
					ЧастиЗапроса.Добавить(
				"		{ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОКлассахУсловийТруда КАК КлассыУсловийТрудаПоДолжностям
				|		ПО КадроваяИсторияСотрудников." + ИмяПоляДолжность + " = КлассыУсловийТрудаПоДолжностям.Должность
				|			И ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = КлассыУсловийТрудаПоДолжностям.Период}");

		КонецЕсли;
		//end fix Клещ А.Н. 14.08.2019
#КонецВставки
		
	Иначе
		
		ЧастиЗапроса.Добавить(
			"		{ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОКлассахУсловийТруда КАК КлассыУсловийТрудаПоДолжностям
			|		ПО ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность = КлассыУсловийТрудаПоДолжностям.Должность
			|			И ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " = КлассыУсловийТрудаПоДолжностям.Период}");
		
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(ЧастиЗапроса, Символы.ПС);
	
	КадровыйУчет.ДобавитьВКоллекциюИмяКадровыхДанных(ИсточникиДанных, "ВТКУничтожению", "ВТСведенияОКлассахУсловийТруда");
	
КонецПроцедуры

