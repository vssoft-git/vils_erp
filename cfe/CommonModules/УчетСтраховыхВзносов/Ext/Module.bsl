&Вместо("СформироватьВТРасширенныхСведенийОДоходах")
// Помещает в переданный МенеджерВременныхТаблиц временные таблицы: 
//	ВТСведенияОДоходах, содержащую данные о доходах, зарегистрированные в учете по страховым взносам, и
//		дополненные сведениями о физлицах и о виде тарифа страховых взносов
// а также таблицы с промежуточными данными, которые могут быть использованы независимо.
//	1. ВТРазмерыТарифныхСтавок, содержащую данные о ставках взносов.
//  2. ВТДанныеОбИнвалидности, содержащую данные о наличии инвалидности и справке об инвалидности.
//  3. ВТИспользуемыеТарифы, содержащую данные о применяемых помесячно тарифах.
//  4. ВТОписаниеТарифов, содержащую данные о необходимости применения особого тарифа для инвалидов для каждого тарифа
//  страховых взносов.
//
// Параметры:
//  НачалоПериода - дата -
//  ОкончаниеПериода - дата -
//  ДатаАктуальности - дата -  
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, куда будет помещены сформированные временные таблицы,
//		содержит вр. таблицу ВТФизЛицаОрганизаций с полями
//			ФизическоеЛицо, должно быть непустым
//			ГоловнаяОрганизация, должно быть непустым.
//  ТолькоРазрешенные - булево - флажок, определяющий, надо ли оставлять в запросе только разрешенные при РЛС записи.
//  ДляПервичнойОтчетностиПФР - булево - флажок, определяющий порядок чтения данных из регистров сведений, если Истина,
//                                       не учитываются строки регистров содержащие опоздавшие до следующего отчетного
//                                       периода данные.
//  ИсключатьДоходыРегистратора - булево - 
//  Регистратор - ДокументСсылка - 
//  УчитыватьОпоздавшиеИзмененияПрименяемыхТарифов - булево - флажок, определяющий будут ли прочитаны изменения применяемых
//													тарифов, внесенные задним числом
//  ОставлятьМесяцыБезДоходов - булево - флажок, определяющий оставлять ли в возвращаемой таблице строки с нулевыми доходами.
//
//
Процедура ВИЛС_СформироватьВТРасширенныхСведенийОДоходах(НачалоПериода, ОкончаниеПериода, ДатаАктуальности, МенеджерВременныхТаблиц, ТолькоРазрешенные, ДляПервичнойОтчетностиПФР = Ложь, ИсключатьДоходыРегистратора = Ложь, Регистратор = Неопределено, УчитыватьОпоздавшиеИзмененияПрименяемыхТарифов = Истина, ОставлятьМесяцыБезДоходов = Ложь) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачалоПериода",		НачалоГода(НачалоПериода));
	Запрос.УстановитьПараметр("ОкончаниеПериода",	ОкончаниеПериода);
	Запрос.УстановитьПараметр("ДатаАктуальности",	ДатаАктуальности);
	Запрос.УстановитьПараметр("НачалоПредшествующегоГода", ДобавитьМесяц(НачалоГода(Мин(НачалоПериода, ДатаАктуальности)), -12));
	Запрос.УстановитьПараметр("ДатаСтрахованияВременноПребывающих",	ДатаСтрахованияВременноПребывающих());
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ?(ИсключатьДоходыРегистратора, Регистратор, Неопределено));
	
	Если НачалоГода(НачалоПериода) >= ДатаВводаЛьготногоТарифаПоНС_ПЗдляИП() Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ИСТИНА КАК ПредоставлятьЛьготуПоИнвалидам
		|ПОМЕСТИТЬ ВТЛьготыПоФСС_НСДляОрганизаций
		|ИЗ
		|	ВТФизЛицаОрганизаций КАК СписокСотрудников";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ИСТИНА КАК ПредоставлятьЛьготуПоИнвалидам
		|ИЗ
		|	ВТФизЛицаОрганизаций КАК СписокСотрудников";
		ВременнаяТаблица = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаТЗ Из ВременнаяТаблица Цикл
			СтрокаТЗ.ПредоставлятьЛьготуПоИнвалидам = ЗарплатаКадры.ЭтоЮридическоеЛицо(СтрокаТЗ.ГоловнаяОрганизация)
		КонецЦикла;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СписокОрганизаций.ГоловнаяОрганизация,
		|	СписокОрганизаций.ПредоставлятьЛьготуПоИнвалидам
		|ПОМЕСТИТЬ ВТЛьготыПоФСС_НСДляОрганизаций
		|ИЗ
		|	&ВременнаяТаблица КАК СписокОрганизаций";
		Запрос.УстановитьПараметр("ВременнаяТаблица", ВременнаяТаблица)
	КонецЕсли;
	Запрос.Выполнить();
	
	// Формируем список месяцев, в разрезе которых собираем данные.
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(Запрос.МенеджерВременныхТаблиц, КонецМесяца(НачалоГода(НачалоПериода)), КонецМесяца(ОкончаниеПериода),,, "ВТПериодыРасширенныхСведенийОДоходах", Истина);
	
	// Получаем временные таблицы, используя общие алгоритмы.
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Периоды.Период КАК Период,
	|	СписокСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СписокСотрудников.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТПериодыФизЛицОрганизаций
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК СписокСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыРасширенныхСведенийОДоходах КАК Периоды
	|		ПО (ИСТИНА)";
	Запрос.Выполнить();
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ВсеЗаписи = Истина;
	ДобавитьОтборПриЧтенииПериодическихДанных(ПараметрыПостроения, ДатаАктуальности, ДляПервичнойОтчетностиПФР И УчитыватьОпоздавшиеИзмененияПрименяемыхТарифов);
		
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ПрименяемыеТарифыСтраховыхВзносов",
		Запрос.МенеджерВременныхТаблиц,
		ТолькоРазрешенные,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТПериодыФизЛицОрганизаций",
			"ГоловнаяОрганизация"),
		ПараметрыПостроения);
		
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ВсеЗаписи = Истина;
	ДобавитьОтборПриЧтенииПериодическихДанных(ПараметрыПостроения, ДатаАктуальности, ДляПервичнойОтчетностиПФР);
		
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"СведенияОбИнвалидностиФизическихЛиц",
		Запрос.МенеджерВременныхТаблиц,
		ТолькоРазрешенные,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТПериодыФизЛицОрганизаций",
			"ФизическоеЛицо"),
		ПараметрыПостроения);
	
	СформироватьВТДанныеОПравахНаСтрахование(Запрос.МенеджерВременныхТаблиц, "ВТПериодыФизЛицОрганизаций", ТолькоРазрешенные, ДатаАктуальности, ДляПервичнойОтчетностиПФР);
	СформироватьВТОписаниеТарифов(НачалоПериода, ОкончаниеПериода, Запрос.МенеджерВременныхТаблиц);

	МассивВидовДохода = Новый Массив;
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ПособияЗаСчетФСС);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ПособияЗаСчетФСС_НС);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ДенежноеДовольствиеВоеннослужащих);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ДенежноеСодержаниеПрокуроров);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.МатпомощьПриРожденииРебенкаПрокуроров);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.МатпомощьПрокуроров);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.КомпенсацииОблагаемыеВзносамиПрокуроров);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.НеОблагаетсяЦеликомПрокуроров);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ДоходыСтудентовЗаРаботуВСтудотрядеПоТрудовомуДоговору);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.НеОблагаетсяЦеликом);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ОблагаетсяЦеликом);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ОблагаетсяЦеликомНеОблагаемыеФСС_НС);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.КомпенсацииОблагаемыеВзносами);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.МатпомощьПриРожденииРебенка);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.Матпомощь);
	Запрос.УстановитьПараметр("ВидыДохода", МассивВидовДохода);
	
	ЗарплатаКадры.СоздатьПустуюВТ(МенеджерВременныхТаблиц, "РегистрНакопления.СведенияОДоходахСтраховыеВзносы");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТДанныеОбИнвалидности", Истина);
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТИспользуемыеТарифы", Истина);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СведенияОДоходахСтраховыеВзносы.Период КАК Период,
	|	СведенияОДоходахСтраховыеВзносы.Регистратор КАК Регистратор,
	|	СведенияОДоходахСтраховыеВзносы.НомерСтроки КАК НомерСтроки,
	|	СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахСтраховыеВзносы.Организация КАК Организация,
	|	СведенияОДоходахСтраховыеВзносы.ВидДохода КАК ВидДохода,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СведенияОДоходахСтраховыеВзносы.ЯвляетсяДоходомФармацевта КАК ЯвляетсяДоходомФармацевта,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяВзносамиНаДоплатуЛетчикам КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяВзносамиНаДоплатуШахтерам КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	СведенияОДоходахСтраховыеВзносы.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ КАК ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	СведенияОДоходахСтраховыеВзносы.ЛьготныйТерриториальныйТариф КАК ЛьготныйТерриториальныйТариф,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	&КлассУсловийТруда,//СведенияОДоходахСтраховыеВзносы.КлассУсловийТруда КАК КлассУсловийТруда,
	|	СведенияОДоходахСтраховыеВзносы.Сумма КАК Сумма,
	|	СведенияОДоходахСтраховыеВзносы.Скидка КАК Скидка,
	|	СведенияОДоходахСтраховыеВзносы.Сотрудник КАК Сотрудник,
	|	СведенияОДоходахСтраховыеВзносы.Начисление КАК Начисление,
	|	СведенияОДоходахСтраховыеВзносы.Подразделение КАК Подразделение,
	|	СведенияОДоходахСтраховыеВзносы.ДатаНачала КАК ДатаНачала,
	|	СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	СведенияОДоходахСтраховыеВзносы.ПоАктуПроверки КАК ПоАктуПроверки
	|ПОМЕСТИТЬ ВТДанныеУчета
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	|ГДЕ
	|	СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПредшествующегоГода И &ДатаАктуальности
	|	И СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо В
	|			(ВЫБРАТЬ
	|				ФизЛицаОрганизаций.ФизическоеЛицо
	|			ИЗ
	|				ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций)
	|	И СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация В
	|			(ВЫБРАТЬ
	|				ФизЛицаОрганизаций.ГоловнаяОрганизация
	|			ИЗ
	|				ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СведенияОДоходахСтраховыеВзносы.Период,
	|	СведенияОДоходахСтраховыеВзносы.Регистратор,
	|	СведенияОДоходахСтраховыеВзносы.НомерСтроки,
	|	СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация,
	|	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо,
	|	СведенияОДоходахСтраховыеВзносы.Организация,
	|	СведенияОДоходахСтраховыеВзносы.ВидДохода,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяЕНВД,
	|	СведенияОДоходахСтраховыеВзносы.ЯвляетсяДоходомФармацевта,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	СведенияОДоходахСтраховыеВзносы.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	СведенияОДоходахСтраховыеВзносы.ЛьготныйТерриториальныйТариф,
	|	СведенияОДоходахСтраховыеВзносы.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	&КлассУсловийТруда,//СведенияОДоходахСтраховыеВзносы.КлассУсловийТруда,
	|	СведенияОДоходахСтраховыеВзносы.Сумма,
	|	СведенияОДоходахСтраховыеВзносы.Скидка,
	|	СведенияОДоходахСтраховыеВзносы.Сотрудник,
	|	СведенияОДоходахСтраховыеВзносы.Начисление,
	|	СведенияОДоходахСтраховыеВзносы.Подразделение,
	|	СведенияОДоходахСтраховыеВзносы.ДатаНачала,
	|	СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода,
	|	СведенияОДоходахСтраховыеВзносы.ПоАктуПроверки
	|ИЗ
	|	ВТРегистрНакопления_СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	|ГДЕ
	|	СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПредшествующегоГода И &ДатаАктуальности
	|	И (СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо, СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация) В
	|			(ВЫБРАТЬ
	|				ФизЛицаОрганизаций.ФизическоеЛицо,
	|				ФизЛицаОрганизаций.ГоловнаяОрганизация
	|			ИЗ
	|				ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СписокСотрудников.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТРазныеФизЛицаОрганизаций
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК СписокСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СведенияОДоходах.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) КАК ПериодРегистрации,
	|	НАЧАЛОПЕРИОДА(СведенияОДоходах.ДатаПолученияДохода, МЕСЯЦ) КАК Период,
	|	СведенияОДоходах.ВидДохода КАК ВидДохода,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	СведенияОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
	|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СведенияОДоходах.Сумма КАК Сумма,
	|	СведенияОДоходах.Скидка КАК Скидка,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ДатаПолученияДохода < &ДатаСтрахованияВременноПребывающих
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ СведенияОДоходах.ЯвляетсяДоходомФармацевта
	|	КОНЕЦ КАК ЯвляетсяДоходомФармацевта,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ДатаПолученияДохода < &ДатаСтрахованияВременноПребывающих
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ
	|	КОНЕЦ КАК ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ДатаПолученияДохода < &ДатаСтрахованияВременноПребывающих
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ПустаяСсылка)
	|		ИНАЧЕ СведенияОДоходах.ЛьготныйТерриториальныйТариф
	|	КОНЕЦ КАК ЛьготныйТерриториальныйТариф,
	|	СведенияОДоходах.Сотрудник КАК Сотрудник,
	|	СведенияОДоходах.Начисление КАК Начисление,
	|	СведенияОДоходах.Регистратор КАК Регистратор,
	|	СведенияОДоходах.НомерСтроки КАК НомерСтроки,
	|	СведенияОДоходах.Подразделение КАК Подразделение,
	|	СведенияОДоходах.ДатаНачала КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией = ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПодземныеИВредныеРаботы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПодземныеИВредныеРаботы,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией = ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ТяжелыеИПрочиеРаботы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТяжелыеИПрочиеРаботы,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПустаяСсылка,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Опасный)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Опасный,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Вредный1,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный2)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Вредный2,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный3)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Вредный3,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный4)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Вредный4,
	|	ЕСТЬNULL(СведенияОДоходах.ВидДохода.ВходитВБазуПФР, ЛОЖЬ) КАК ВидДоходаВходитВБазуПФР,
	|	ЕСТЬNULL(СведенияОДоходах.ВидДохода.ВходитВБазуФСС, ЛОЖЬ) КАК ВидДоходаВходитВБазуФСС,
	|	ЕСТЬNULL(СведенияОДоходах.ВидДохода.ВходитВБазуФСС_НС, ЛОЖЬ) КАК ВидДоходаВходитВБазуФСС_НС,
	|	ЕСТЬNULL(СведенияОДоходах.ВидДохода.ВходитВБазуФОМС, ЛОЖЬ) КАК ВидДоходаВходитВБазуФОМС,
	|	ЕСТЬNULL(СведенияОДоходах.ВидДохода.АвторскиеВознаграждения, ЛОЖЬ) КАК ВидДоходаАвторскиеВознаграждения,
	|	ВЫБОР
	|		КОГДА НЕ СведенияОДоходах.ПоАктуПроверки
	|				И НАЧАЛОПЕРИОДА(СведенияОДоходах.ДатаПолученияДохода, КВАРТАЛ) >= НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВключатьВПервичнуюОтчетностьПФР
	|ПОМЕСТИТЬ ВТДоходыПоФизлицамМесяцам
	|ИЗ
	|	ВТРазныеФизЛицаОрганизаций КАК ФизЛицаОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеУчета КАК СведенияОДоходах
	|		ПО ФизЛицаОрганизаций.ФизическоеЛицо = СведенияОДоходах.ФизическоеЛицо
	|			И ФизЛицаОрганизаций.ГоловнаяОрганизация = СведенияОДоходах.ГоловнаяОрганизация
	|ГДЕ
	|	СведенияОДоходах.ВидДохода <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ПустаяСсылка)
	|	И СведенияОДоходах.Регистратор <> &ИсключаемыйРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(СведенияОбИнвалидностиФизическихЛиц.Период, МЕСЯЦ) КАК Период,
	|	СведенияОбИнвалидностиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(СведенияОбИнвалидностиФизическихЛиц.Инвалидность, ЛОЖЬ) КАК Инвалид,
	|	СведенияОбИнвалидностиФизическихЛиц.ДатаВыдачи КАК СправкаОбИнвалидностиДатаВыдачи,
	|	СведенияОбИнвалидностиФизическихЛиц.СрокДействияСправки КАК СправкаОбИнвалидностиСрокДействия
	|ПОМЕСТИТЬ ВТДанныеОбИнвалидности
	|ИЗ
	|	ВТСведенияОбИнвалидностиФизическихЛицСрезПоследних КАК СведенияОбИнвалидностиФизическихЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ПрименяемыеТарифыСтраховыхВзносов.Период, МЕСЯЦ) КАК Период,
	|	ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОписаниеТарифов.ЗаменятьОсновнымТарифом, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим)
	|		ИНАЧЕ ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа, ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим))
	|	КОНЕЦ КАК ВидТарифаСтраховыхВзносов
	|ПОМЕСТИТЬ ВТИспользуемыеТарифы
	|ИЗ
	|	ВТПрименяемыеТарифыСтраховыхВзносовСрезПоследних КАК ПрименяемыеТарифыСтраховыхВзносов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОписаниеТарифов КАК ОписаниеТарифов
	|		ПО ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа = ОписаниеТарифов.ВидТарифа
	|			И (ГОД(ПрименяемыеТарифыСтраховыхВзносов.Период) = ОписаниеТарифов.РасчетныйПериод)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) КАК Период,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ПериодРегистрации, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ПериодРегистрации,
	|	СписокФизЛиц.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ЕСТЬNULL(ДоходыПоФизлицам.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	СписокФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ГОД(СписокФизЛиц.ФизическоеЛицо.ДатаРождения) < 1967
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДанныеОПравеНаСтрахование.ВидЗастрахованногоЛица = ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцы)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДанныеОПравеНаСтрахование.ВидЗастрахованногоЛица = ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДанныеОПравеНаСтрахование.ВидЗастрахованногоЛица = ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыПризнанныеБеженцами)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДанныеОПравеНаСтрахование.ВидЗастрахованногоЛица = ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСДолгосрочнымиДоговорами)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДанныеОПравеНаСтрахование.ВидЗастрахованногоЛица = ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПроживающиеИностранцы)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДанныеОПравеНаСтрахование.ВидЗастрахованногоЛица = ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВысококвалифицированныеСпециалистыВременноПроживающие)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НачислятьНакопительнуюЧасть,
	|	СписокФизЛиц.ФизическоеЛицо.ФИО КАК ФизическоеЛицоНаименование,
	|	ДанныеОбИнвалидности.Инвалид КАК Инвалид,
	|	ВЫБОР
	|		КОГДА НЕ ЛьготыПоФСС_НСДляОрганизаций.ПредоставлятьЛьготуПоИнвалидам
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДанныеОбИнвалидности.Инвалид
	|			ТОГДА ИСТИНА
	|		КОГДА ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.ВидТарифа = ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
	|	ДанныеОПравеНаСтрахование.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица,
	|	ДанныеОПравеНаСтрахование.ЗастрахованПФР КАК ЗастрахованПФР,
	|	ДанныеОПравеНаСтрахование.ЗастрахованФОМС КАК ЗастрахованФОМС,
	|	ДанныеОПравеНаСтрахование.ЗастрахованФСС КАК ЗастрахованФСС,
	|	ДанныеОПравеНаСтрахование.ЗастрахованФССкакИностранец КАК ЗастрахованФССкакИностранец,
	|	ЕСТЬNULL(ДоходыПоФизлицам.Сотрудник, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ДанныеОбИнвалидности.Инвалид
	|				И ОписаниеТарифов.ВыделятьДоходыИнвалидов
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов)
	|		КОГДА ЕСТЬNULL(ОписаниеТарифов.ЗаменятьТарифНаЕНВД, ЛОЖЬ)
	|				И ДоходыПоФизлицам.ОблагаетсяЕНВД
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ЕНВД)
	|		КОГДА ЕСТЬNULL(ОписаниеТарифов.ЗаменятьОсновнымТарифом, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим)
	|		КОГДА ДоходыПоФизлицам.ЯвляетсяДоходомФармацевта
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ДляФармацевтовАптек)
	|		КОГДА ДоходыПоФизлицам.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ДляЧленовЭкипажейМорскихСудовПодФлагомРФ)
	|		КОГДА ДоходыПоФизлицам.ЛьготныйТерриториальныйТариф <> ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ПустаяСсылка)
	|			ТОГДА ДоходыПоФизлицам.ЛьготныйТерриториальныйТариф
	|		ИНАЧЕ ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов
	|	КОНЕЦ КАК ВидТарифаСтраховыхВзносов,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ОблагаетсяЕНВД, ЛОЖЬ) КАК ОблагаетсяЕНВД,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ВидДохода, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ПустаяСсылка)) КАК ВидДохода,
	|	ЕСТЬNULL(ДоходыПоФизлицам.Начисление, ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка)) КАК Начисление,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР, ЛОЖЬ) КАК ВходитВБазуПФР,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ВидДоходаВходитВБазуФСС, ЛОЖЬ) КАК ВходитВБазуФСС,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ВидДоходаВходитВБазуФСС_НС, ЛОЖЬ) КАК ВходитВБазуФСС_НС,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ВидДоходаВходитВБазуФОМС, ЛОЖЬ) КАК ВходитВБазуФОМС,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ОблагаетсяВзносамиНаДоплатуШахтерам, ЛОЖЬ) КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией, ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)) КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ЕСТЬNULL(ДоходыПоФизлицам.КлассУсловийТруда, ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка)) КАК КлассУсловийТруда,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ОблагаетсяВзносамиНаДоплатуЛетчикам, ЛОЖЬ) КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ДоходыПоФизлицам.ВидДохода В (&ВидыДохода)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.Трудовой)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ГражданскоПравовой)
	|		КОНЕЦ, ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ПустаяСсылка)) КАК ТипДоговора,
	|	ЕСТЬNULL(ДоходыПоФизлицам.Сумма, 0) КАК Сумма,
	|	ЕСТЬNULL(ДоходыПоФизлицам.Скидка, 0) КАК Скидка,
	|	ВЫБОР
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ЕСТЬNULL(ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР, ЛОЖЬ)
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаПФР,
	|	ВЫБОР
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованФОМС
	|				И ЕСТЬNULL(ДоходыПоФизлицам.ВидДоходаВходитВБазуФОМС, ЛОЖЬ)
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаФОМС,
	|	ВЫБОР
	|		КОГДА (ДанныеОПравеНаСтрахование.ЗастрахованФСС
	|				ИЛИ ДанныеОПравеНаСтрахование.ЗастрахованФССкакИностранец)
	|				И ЕСТЬNULL(ДоходыПоФизлицам.ВидДоходаВходитВБазуФСС, ЛОЖЬ)
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаФСС,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДоходыПоФизлицам.ВидДоходаВходитВБазуФСС_НС
	|			ТОГДА ДоходыПоФизлицам.Сумма - ВЫБОР
	|					КОГДА ДоходыПоФизлицам.ВидДоходаАвторскиеВознаграждения
	|						ТОГДА 0
	|					ИНАЧЕ ДоходыПоФизлицам.Скидка
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаФСС_НС,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ПодземныеИВредныеРаботы
	|				И ДоходыПоФизлицам.ПустаяСсылка
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаПодземныеИВредныеРаботы,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ТяжелыеИПрочиеРаботы
	|				И ДоходыПоФизлицам.ПустаяСсылка
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаТяжелыеИПрочиеРаботы,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ПодземныеИВредныеРаботы
	|				И ДоходыПоФизлицам.Опасный
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаПодземныеИВредныеРаботыОпасный,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ТяжелыеИПрочиеРаботы
	|				И ДоходыПоФизлицам.Опасный
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаТяжелыеИПрочиеРаботыОпасный,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ПодземныеИВредныеРаботы
	|				И ДоходыПоФизлицам.Вредный1
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаПодземныеИВредныеРаботыВредный1,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ТяжелыеИПрочиеРаботы
	|				И ДоходыПоФизлицам.Вредный1
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаТяжелыеИПрочиеРаботыВредный1,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ПодземныеИВредныеРаботы
	|				И ДоходыПоФизлицам.Вредный2
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаПодземныеИВредныеРаботыВредный2,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ТяжелыеИПрочиеРаботы
	|				И ДоходыПоФизлицам.Вредный2
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаТяжелыеИПрочиеРаботыВредный2,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ПодземныеИВредныеРаботы
	|				И ДоходыПоФизлицам.Вредный3
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаПодземныеИВредныеРаботыВредный3,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ТяжелыеИПрочиеРаботы
	|				И ДоходыПоФизлицам.Вредный3
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаТяжелыеИПрочиеРаботыВредный3,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ПодземныеИВредныеРаботы
	|				И ДоходыПоФизлицам.Вредный4
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаПодземныеИВредныеРаботыВредный4,
	|	ВЫБОР
	|		КОГДА ДоходыПоФизлицам.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ДанныеОПравеНаСтрахование.ЗастрахованПФР
	|				И ДоходыПоФизлицам.ВидДоходаВходитВБазуПФР
	|				И ДоходыПоФизлицам.ТяжелыеИПрочиеРаботы
	|				И ДоходыПоФизлицам.Вредный4
	|			ТОГДА ДоходыПоФизлицам.Сумма - ДоходыПоФизлицам.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БазаТяжелыеИПрочиеРаботыВредный4,
	|	ЕСТЬNULL(ДоходыПоФизлицам.Регистратор, НЕОПРЕДЕЛЕНО) КАК Регистратор,
	|	ЕСТЬNULL(ДоходыПоФизлицам.НомерСтроки, 0) КАК НомерСтроки,
	|	ЕСТЬNULL(ДоходыПоФизлицам.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаНачала,
	|	ЕСТЬNULL(ДоходыПоФизлицам.ВключатьВПервичнуюОтчетностьПФР, ИСТИНА) КАК ВключатьВПервичнуюОтчетностьПФР
	|ПОМЕСТИТЬ ВТСведенияБезУточненияЗастрахованностиИностранцевВФСС
	|ИЗ
	|	ВТПериодыФизЛицОрганизаций КАК СписокФизЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоФизлицамМесяцам КАК ДоходыПоФизлицам
	|		ПО СписокФизЛиц.ГоловнаяОрганизация = ДоходыПоФизлицам.ГоловнаяОрганизация
	|			И СписокФизЛиц.ФизическоеЛицо = ДоходыПоФизлицам.ФизическоеЛицо
	|			И (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = ДоходыПоФизлицам.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользуемыеТарифы КАК ИспользуемыеТарифы
	|		ПО СписокФизЛиц.ГоловнаяОрганизация = ИспользуемыеТарифы.ГоловнаяОрганизация
	|			И (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = ИспользуемыеТарифы.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПрименяемыеТарифыСтраховыхВзносовСрезПоследних КАК ПрименяемыеТарифыСтраховыхВзносовСрезПоследних
	|		ПО СписокФизЛиц.ГоловнаяОрганизация = ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.ГоловнаяОрганизация
	|			И (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПрименяемыеТарифыСтраховыхВзносовСрезПоследних.Период, МЕСЯЦ))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЛьготыПоФСС_НСДляОрганизаций КАК ЛьготыПоФСС_НСДляОрганизаций
	|		ПО СписокФизЛиц.ГоловнаяОрганизация = ЛьготыПоФСС_НСДляОрганизаций.ГоловнаяОрганизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОписаниеТарифов КАК ОписаниеТарифов
	|		ПО (ВЫБОР
	|				КОГДА ДоходыПоФизлицам.ЯвляетсяДоходомФармацевта
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ДляФармацевтовАптек)
	|				КОГДА ДоходыПоФизлицам.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ДляЧленовЭкипажейМорскихСудовПодФлагомРФ)
	|				КОГДА ДоходыПоФизлицам.ЛьготныйТерриториальныйТариф <> ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ПустаяСсылка)
	|					ТОГДА ДоходыПоФизлицам.ЛьготныйТерриториальныйТариф
	|				ИНАЧЕ ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов
	|			КОНЕЦ = ОписаниеТарифов.ВидТарифа)
	|			И (ГОД(СписокФизЛиц.Период) = ОписаниеТарифов.РасчетныйПериод)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК ДанныеОбИнвалидности
	|		ПО (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = ДанныеОбИнвалидности.Период)
	|			И СписокФизЛиц.ФизическоеЛицо = ДанныеОбИнвалидности.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОПравахНаСтрахование КАК ДанныеОПравеНаСтрахование
	|		ПО (НАЧАЛОПЕРИОДА(СписокФизЛиц.Период, МЕСЯЦ) = ДанныеОПравеНаСтрахование.Период)
	|			И СписокФизЛиц.ФизическоеЛицо = ДанныеОПравеНаСтрахование.ФизическоеЛицо
	|			И СписокФизЛиц.ГоловнаяОрганизация = ДанныеОПравеНаСтрахование.ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДоходыПоФизлицамМесяцам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТРазныеФизЛицаОрганизаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДанныеУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТТарифыСтраховыхВзносовСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПрименяемыеТарифыСтраховыхВзносовСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСведенияОбИнвалидностиФизическихЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДанныеОПравахНаСтрахование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПериодыФизЛицОрганизаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТЛьготыПоФСС_НСДляОрганизаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПериодыРасширенныхСведенийОДоходах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходах.Период КАК Период,
	|	СведенияОДоходах.ПериодРегистрации КАК ПериодРегистрации,
	|	СведенияОДоходах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СведенияОДоходах.Организация КАК Организация,
	|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходах.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
	|	СведенияОДоходах.ФизическоеЛицоНаименование КАК ФизическоеЛицоНаименование,
	|	СведенияОДоходах.Инвалид КАК Инвалид,
	|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
	|	СведенияОДоходах.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица,
	|	СведенияОДоходах.ЗастрахованПФР КАК ЗастрахованПФР,
	|	СведенияОДоходах.ЗастрахованФОМС КАК ЗастрахованФОМС,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ЗастрахованФСС
	|			ТОГДА ИСТИНА
	|		КОГДА СведенияОДоходах.ЗастрахованФССкакИностранец
	|				И НЕ ОписаниеТарифов.ВыделятьДоходыИностранцевДляФСС
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗастрахованФСС,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ЗастрахованФССкакИностранец
	|				И ОписаниеТарифов.ВыделятьДоходыИностранцевДляФСС
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗастрахованФССкакИностранец,
	|	СведенияОДоходах.Сотрудник КАК Сотрудник,
	|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
	|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СведенияОДоходах.ВидДохода КАК ВидДохода,
	|	СведенияОДоходах.Начисление КАК Начисление,
	|	СведенияОДоходах.ВходитВБазуПФР КАК ВходитВБазуПФР,
	|	СведенияОДоходах.ВходитВБазуФСС КАК ВходитВБазуФСС,
	|	СведенияОДоходах.ВходитВБазуФОМС КАК ВходитВБазуФОМС,
	|	СведенияОДоходах.ВходитВБазуФСС_НС КАК ВходитВБазуФСС_НС,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	СведенияОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	СведенияОДоходах.ТипДоговора КАК ТипДоговора,
	|	СУММА(СведенияОДоходах.Сумма) КАК Сумма,
	|	СУММА(СведенияОДоходах.Скидка) КАК Скидка,
	|	СУММА(СведенияОДоходах.БазаПФР) КАК БазаПФР,
	|	СУММА(СведенияОДоходах.БазаФОМС) КАК БазаФОМС,
	|	СУММА(СведенияОДоходах.БазаФСС) КАК БазаФСС,
	|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
	|	СУММА(СведенияОДоходах.БазаПодземныеИВредныеРаботы) КАК БазаПодземныеИВредныеРаботы,
	|	СУММА(СведенияОДоходах.БазаТяжелыеИПрочиеРаботы) КАК БазаТяжелыеИПрочиеРаботы,
	|	СУММА(СведенияОДоходах.БазаПодземныеИВредныеРаботыОпасный) КАК БазаПодземныеИВредныеРаботыОпасный,
	|	СУММА(СведенияОДоходах.БазаТяжелыеИПрочиеРаботыОпасный) КАК БазаТяжелыеИПрочиеРаботыОпасный,
	|	СУММА(СведенияОДоходах.БазаПодземныеИВредныеРаботыВредный1) КАК БазаПодземныеИВредныеРаботыВредный1,
	|	СУММА(СведенияОДоходах.БазаТяжелыеИПрочиеРаботыВредный1) КАК БазаТяжелыеИПрочиеРаботыВредный1,
	|	СУММА(СведенияОДоходах.БазаПодземныеИВредныеРаботыВредный2) КАК БазаПодземныеИВредныеРаботыВредный2,
	|	СУММА(СведенияОДоходах.БазаТяжелыеИПрочиеРаботыВредный2) КАК БазаТяжелыеИПрочиеРаботыВредный2,
	|	СУММА(СведенияОДоходах.БазаПодземныеИВредныеРаботыВредный3) КАК БазаПодземныеИВредныеРаботыВредный3,
	|	СУММА(СведенияОДоходах.БазаТяжелыеИПрочиеРаботыВредный3) КАК БазаТяжелыеИПрочиеРаботыВредный3,
	|	СУММА(СведенияОДоходах.БазаПодземныеИВредныеРаботыВредный4) КАК БазаПодземныеИВредныеРаботыВредный4,
	|	СУММА(СведенияОДоходах.БазаТяжелыеИПрочиеРаботыВредный4) КАК БазаТяжелыеИПрочиеРаботыВредный4,
	|	0 КАК СуммаПревысившаяПределПФР,
	|	0 КАК СуммаПревысившаяПределФСС,
	|	0 КАК СуммаПревысившаяПределФОМС,
	|	СведенияОДоходах.Регистратор КАК Регистратор,
	|	СведенияОДоходах.НомерСтроки КАК НомерСтроки,
	|	СведенияОДоходах.Подразделение КАК Подразделение,
	|	СведенияОДоходах.ДатаНачала КАК ДатаНачала,
	|	СведенияОДоходах.ВключатьВПервичнуюОтчетностьПФР КАК ВключатьВПервичнуюОтчетностьПФР
	|ПОМЕСТИТЬ ВТСведенияОДоходах
	|ИЗ
	|	ВТСведенияБезУточненияЗастрахованностиИностранцевВФСС КАК СведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОписаниеТарифов КАК ОписаниеТарифов
	|		ПО СведенияОДоходах.ВидТарифаСтраховыхВзносов = ОписаниеТарифов.ВидТарифа
	|			И (ГОД(СведенияОДоходах.Период) = ОписаниеТарифов.РасчетныйПериод)
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходах.Период,
	|	СведенияОДоходах.ПериодРегистрации,
	|	СведенияОДоходах.ГоловнаяОрганизация,
	|	СведенияОДоходах.Организация,
	|	СведенияОДоходах.ФизическоеЛицо,
	|	СведенияОДоходах.НачислятьНакопительнуюЧасть,
	|	СведенияОДоходах.ФизическоеЛицоНаименование,
	|	СведенияОДоходах.Инвалид,
	|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС,
	|	СведенияОДоходах.ВидЗастрахованногоЛица,
	|	СведенияОДоходах.ЗастрахованПФР,
	|	СведенияОДоходах.ЗастрахованФОМС,
	|	СведенияОДоходах.Сотрудник,
	|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	СведенияОДоходах.ВидДохода,
	|	СведенияОДоходах.Начисление,
	|	СведенияОДоходах.ВходитВБазуПФР,
	|	СведенияОДоходах.ВходитВБазуФСС,
	|	СведенияОДоходах.ВходитВБазуФОМС,
	|	СведенияОДоходах.ВходитВБазуФСС_НС,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	СведенияОДоходах.КлассУсловийТруда,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	СведенияОДоходах.ТипДоговора,
	|	СведенияОДоходах.Регистратор,
	|	СведенияОДоходах.Подразделение,
	|	СведенияОДоходах.ДатаНачала,
	|	СведенияОДоходах.ВключатьВПервичнуюОтчетностьПФР,
	|	СведенияОДоходах.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ЗастрахованФСС
	|			ТОГДА ИСТИНА
	|		КОГДА СведенияОДоходах.ЗастрахованФССкакИностранец
	|				И НЕ ОписаниеТарифов.ВыделятьДоходыИностранцевДляФСС
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СведенияОДоходах.ЗастрахованФССкакИностранец
	|				И ОписаниеТарифов.ВыделятьДоходыИностранцевДляФСС
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	(СУММА(СведенияОДоходах.Сумма) <> 0
	|		ИЛИ СУММА(СведенияОДоходах.Скидка) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСведенияБезУточненияЗастрахованностиИностранцевВФСС";
	
	//begin fix Клещ А.Н. 12.09.2019 &КлассУсловийТруда,//СведенияОДоходахСтраховыеВзносы.КлассУсловийТруда КАК КлассУсловийТруда, 
	ТекстЗапроса = ?(НачалоПериода <Дата('20200101'),СтрЗаменить(ТекстЗапроса,"&КлассУсловийТруда","Значение(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка) КАК КлассУсловийТруда"),СтрЗаменить(ТекстЗапроса,"&КлассУсловийТруда","СведенияОДоходахСтраховыеВзносы.КлассУсловийТруда КАК КлассУсловийТруда"));
	//end fix Клещ А.Н. 12.09.2019
		
	Если Не ТолькоРазрешенные Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	Если ОставлятьМесяцыБезДоходов Тогда
		НачальнаяПозиция = СтрНайти(ТекстЗапроса, "ИМЕЮЩИЕ");
		ПоследняяПозиция = СтрНайти(ТекстЗапроса, ";", , НачальнаяПозиция) - 1;
		ТекстЗапроса = Лев(ТекстЗапроса, НачальнаяПозиция - 1) + Прав(ТекстЗапроса, СтрДлина(ТекстЗапроса) - ПоследняяПозиция);
	КонецЕсли;   
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
КонецПроцедуры
