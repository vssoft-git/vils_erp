
&ИзменениеИКонтроль("ВывестиВКоллекциюПечатнуюФорму")
Процедура ВИЛС_ВывестиВКоллекциюПечатнуюФорму(ИмяОтчета, МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, ВнешниеНаборыДанных = Неопределено, ДополнительныеПараметрыФормирования = Неопределено) Экспорт
	
	ВариантыОтчетовПечатныхФорм = МакетыВариантовОтчетовПечатныхФорм();
	Для Каждого ОписаниеНастраиваемойФормы Из ВариантыОтчетовПечатныхФорм Цикл
		
		ПутьКВариантуОтчета = ОписаниеНастраиваемойФормы.Ключ;
		Если СтрНайти(ПутьКВариантуОтчета, ИмяОтчета) = 1 Тогда
			
			ПутьКМакетуПечатнойФормы = ОписаниеНастраиваемойФормы.Значение;
			ИмяМакетаПечатнойФормы = ИмяМакетаИзПутиКМакетуПечатнойФормы(ПутьКМакетуПечатнойФормы);
			
			Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакетаПечатнойФормы) Тогда
				
				Если ДополнительныеПараметрыФормирования = Неопределено Тогда
					ДополнительныеПараметры = Новый Структура;
				Иначе
					ДополнительныеПараметры = ОбщегоНазначения.СкопироватьРекурсивно(ДополнительныеПараметрыФормирования);
				КонецЕсли;
				
				ДокументРезультат = Новый ТабличныйДокумент;
				
				КлючВарианта = КлючВариантаИзПутиКВарианту(ПутьКВариантуОтчета);
				
				Если ВРег(Лев(ПутьКМакетуПечатнойФормы, 11)) = ВРег("ОбщийМакет.") Тогда
					ПредставлениеПечатнойФормы = Метаданные.ОбщиеМакеты[ИмяМакетаПечатнойФормы].Синоним;
				Иначе
					ПредставлениеПечатнойФормы = Метаданные.НайтиПоПолномуИмени(ИмяОтчета).Макеты[ИмяМакетаПечатнойФормы].Синоним;
				КонецЕсли;
				
				ОбъектОтчета = ОбщегоНазначения.ОбъектПоПолномуИмени(ИмяОтчета);
				
				Если ДополнительныеПараметры.Свойство("Отбор") Тогда
					МакетКомпоновкиДанных = Неопределено;
				Иначе
					МакетКомпоновкиДанных = МакетКомпоновкиДанныхОтчета(ОбъектОтчета, КлючВарианта);
				КонецЕсли;
#Вставка				

				// begin fix Suetin 08.08.2019 15:36:18
				ПереопределитьВычисляемоеПоле 				= Неопределено;
				ПереопределяемоеВычисляемоеПоле 			= Неопределено;
				ПереопределяемоеВыражениеВычисляемогоПоля 	= Неопределено;
				Если ПараметрыПечати.Свойство("ПереопределитьВычисляемоеПоле", ПереопределитьВычисляемоеПоле) и ПереопределитьВычисляемоеПоле Тогда
					Если ПараметрыПечати.Свойство("ПереопределяемоеВычисляемоеПоле", ПереопределяемоеВычисляемоеПоле) и ЗначениеЗаполнено(ПереопределяемоеВычисляемоеПоле) Тогда	
						ПереопределяемоеВычисляемоеПоле = ОбъектОтчета.СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти(ПереопределяемоеВычисляемоеПоле);
						Если ПараметрыПечати.Свойство("ПереопределяемоеВыражениеВычисляемогоПоля", ПереопределяемоеВыражениеВычисляемогоПоля) и ЗначениеЗаполнено(ПереопределяемоеВыражениеВычисляемогоПоля) Тогда
							ПереопределяемоеВычисляемоеПоле.Выражение = ПереопределяемоеВыражениеВычисляемогоПоля;	
							МакетКомпоновкиДанных = Неопределено;
							ПереопределяемоеВыражениеВычисляемогоПоля 	= Неопределено;
						КонецЕсли;	
						ПереопределяемоеВычисляемоеПоле 	= Неопределено;
					КонецЕсли;
					ПереопределитьВычисляемоеПоле 			= Неопределено;
				КонецЕсли;	
				// end fix Suetin 08.08.2019 15:36:25
#КонецВставки
				
				Если МакетКомпоновкиДанных = Неопределено Тогда
					
					НастройкиОтчетаКД = Неопределено;
					ПользовательскиеНастройкиКД = Неопределено;
					
					ИнициализироватьНастройкиОтчета(ОбъектОтчета, "", КлючВарианта, НастройкиОтчетаКД, ПользовательскиеНастройкиКД);
					
					Если НастройкиОтчетаКД = Неопределено Тогда
						НастройкиОтчетаКД = ОбъектОтчета.СхемаКомпоновкиДанных.ВариантыНастроек[КлючВарианта].Настройки;
					КонецЕсли;
					
					ОбъектОтчета.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчетаКД);
					Если ТипЗнч(ПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
						ОбъектОтчета.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройкиКД);
					КонецЕсли;
					
					УстановитьПараметрОтбора(ОбъектОтчета, МассивОбъектов);
					
				Иначе
					
					ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить(
						"МакетКомпоновкиДанных", МакетКомпоновкиДанных);
					
					ПараметрКлючВарианта = МакетКомпоновкиДанных.ЗначенияПараметров.Найти("КлючВарианта");
					Если ПараметрКлючВарианта <> Неопределено Тогда
						ДополнительныеПараметры.Вставить("КлючВарианта", ПараметрКлючВарианта.Значение);
					КонецЕсли;
					
					Для каждого Параметр Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл
						
						ПараметрКомпоновщика = Лев(Параметр.Имя, 1) = "П" И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Сред(Параметр.Имя, 2));
						
						Если Параметр.Имя = "СсылкиНаОбъекты"
							Или ПараметрКомпоновщика
								И ТипЗнч(Параметр.Значение) = Тип("СписокЗначений")
								И Не ЗначениеЗаполнено(Параметр.Значение) Тогда
							
							Параметр.Значение.ЗагрузитьЗначения(МассивОбъектов);
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				РезультатКомпоновки = РезультатКомпоновкиМакетаПечатнойФормы(ОбъектОтчета, , , ВнешниеНаборыДанных, ДополнительныеПараметры);
				
				МенеджерОтчета = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяОтчета);
				МенеджерОтчета.Сформировать(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати);
				
				УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
					КоллекцияПечатныхФорм,
					ИмяМакетаПечатнойФормы,
					ПредставлениеПечатнойФормы,
					ДокументРезультат,,);
				
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
