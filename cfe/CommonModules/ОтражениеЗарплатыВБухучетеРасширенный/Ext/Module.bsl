
&ИзменениеИКонтроль("ЗаполнитьПараметрыДляРасчетаОценочныхОбязательствОтпусков")
// Заполняет параметры для расчета оценочных обязательств по отпускам.
//
// Параметры:
// 	Организация – тип СправочникСсылка.Организации.
// 	ПериодРегистрации – тип Дата.
// 	ПараметрыДляРасчета – структура, параметры, которые будут заполняться, описание см ОтражениеЗарплатыВБухучете.ОписаниеПараметровДляРасчетаОценочныхОбязательствОтпусков.
// 	Сотрудники – Неопределенно или Массив.
//
Процедура ВИЛС_ЗаполнитьПараметрыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, ПараметрыДляРасчета, Сотрудники) Экспорт

	ПодработкаСотрудник = Новый Соответствие;
	СотрудникиДляНачисленийИВзносов = Сотрудники;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И Сотрудники <> Неопределено Тогда
		
		// Для получения начислений и взносов дополним сотрудников подработками.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнойСотрудник КАК ГоловнойСотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ГоловнойСотрудник В(&Сотрудники)
		|	И Сотрудники.Ссылка <> Сотрудники.ГоловнойСотрудник";
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			СотрудникиДляНачисленийИВзносов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Сотрудники);
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ПодработкаСотрудник.Вставить(Выборка.Сотрудник, Выборка.ГоловнойСотрудник);
				СотрудникиДляНачисленийИВзносов.Добавить(Выборка.Сотрудник);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НачисленнаяЗарплатаИВзносы = НачисленияИВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, СотрудникиДляНачисленийИВзносов);
#Вставка	
	//begin fix Клещ А.Н. 06.05.2019  
	ВИЛС_ИзменениеОтраженияВБухУчете(НачисленнаяЗарплатаИВзносы);
	//end fix Клещ А.Н. 06.05.2019
#КонецВставки
	ФондОплатыТрудаИСтраховыеВзносы = ОтражениеЗарплатыВБухучете.ФондОплатыТрудаИСтраховыеВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, НачисленнаяЗарплатаИВзносы);
	УдалитьСтрокиИсключаемыеИзРасчетаСреднегоЗаработка(ФондОплатыТрудаИСтраховыеВзносы, Организация, ПериодРегистрации);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ФондОплатыТрудаИСтраховыеВзносы, ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	
	ОстаткиОтпусковДляРасчетаОценочныхОбязательств = ОтражениеЗарплатыВБухучете.ОстаткиОтпусковДляРасчетаОценочныхОбязательств(Организация, ПериодРегистрации, Сотрудники);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ОстаткиОтпусковДляРасчетаОценочныхОбязательств, ПараметрыДляРасчета.ОстаткиОтпусков);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		ПараметрыДляРасчета.ОстаткиОтпусков.ЗаполнитьЗначения(КонецМесяца(ПериодРегистрации),"ДатаНачала");
		ИменаТаблиц = "ФондОплатыТрудаИСтраховыеВзносы,ОстаткиОтпусков";
		Модуль.ДополнитьДанныеДокументаМестомВСтруктуреПредприятия(ПараметрыДляРасчета, ИменаТаблиц);
	КонецЕсли;
	
	Для каждого ЭлементСтруктуры Из ПараметрыДляРасчета Цикл
		Если ЭлементСтруктуры.Значение.Колонки.Найти("ПодразделениеУчетаЗатрат") <> Неопределено Тогда
			ЭлементСтруктуры.Значение.Колонки.Удалить("Подразделение");
			ЭлементСтруктуры.Значение.Колонки.ПодразделениеУчетаЗатрат.Имя = "Подразделение";
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И Сотрудники = Неопределено Тогда
		
		СотрудникиНачисленийИВзносов = ОбщегоНазначения.ВыгрузитьКолонку(ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы, "Сотрудник", Истина);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", СотрудникиНачисленийИВзносов);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнойСотрудник КАК ГоловнойСотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&Сотрудники)
		|	И Сотрудники.Ссылка <> Сотрудники.ГоловнойСотрудник";
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ПодработкаСотрудник.Вставить(Выборка.Сотрудник, Выборка.ГоловнойСотрудник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПодработкаСотрудник.Количество() > 0 Тогда
		Для каждого СтрокаТЗ Из ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы Цикл
			ГоловнойСотрудник = ПодработкаСотрудник[СтрокаТЗ.Сотрудник];
			Если ГоловнойСотрудник <> Неопределено Тогда
				СтрокаТЗ.Сотрудник = ГоловнойСотрудник;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Не выполняем свертку таблицы ОстаткиОтпусков, т.к. в ней могут быть сотрудники с нулевыми остатками.
	ВременныеПараметры = Новый Структура;
	ВременныеПараметры.Вставить("ФондОплатыТрудаИСтраховыеВзносы", ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	ОтражениеЗарплатыВБухучете.СвернутьДанныеДляОтраженияЗарплатыВБухучете(ВременныеПараметры, "ДатаНачала,Начисление");
	
	ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы = ВременныеПараметры.ФондОплатыТрудаИСтраховыеВзносы;
	ПараметрыДляРасчета.ОстаткиОтпусков.Колонки.Удалить("ДатаНачала");

КонецПроцедуры

Процедура ВИЛС_ИзменениеОтраженияВБухУчете(НачисленнаяЗарплатаИВзносы)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЗ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТЗ.Сотрудник КАК Сотрудник,
	|	ТЗ.Подразделение КАК Подразделение,
	|	ТЗ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	ТЗ.ДатаНачала КАК ДатаНачала,
	|	ТЗ.ВидОперации КАК ВидОперации,
	|	ТЗ.Начисление КАК Начисление,
	|	ТЗ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ТЗ.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ТЗ.СтатьяРасходов КАК СтатьяРасходов,
	|	ТЗ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ТЗ.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	ТЗ.ПериодПринятияРасходов КАК ПериодПринятияРасходов,
	|	ТЗ.Сумма КАК Сумма,
	|	ТЗ.ПФРДоПредельнойВеличины КАК ПФРДоПредельнойВеличины,
	|	ТЗ.ПФРСПревышения КАК ПФРСПревышения,
	|	ТЗ.ПФРПоСуммарномуТарифу КАК ПФРПоСуммарномуТарифу,
	|	ТЗ.ПФРСтраховая КАК ПФРСтраховая,
	|	ТЗ.ПФРНакопительная КАК ПФРНакопительная,
	|	ТЗ.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	ТЗ.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
	|	ТЗ.ПФРНаДоплатуЛетчикам КАК ПФРНаДоплатуЛетчикам,
	|	ТЗ.ПФРНаДоплатуШахтерам КАК ПФРНаДоплатуШахтерам,
	|	ТЗ.ФСС КАК ФСС,
	|	ТЗ.ФССНесчастныеСлучаи КАК ФССНесчастныеСлучаи,
	|	ТЗ.ФФОМС КАК ФФОМС,
	|	ТЗ.ТФОМС КАК ТФОМС,
	|	ТЗ.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки,
	|	ТЗ.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки,
	|	ТЗ.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка,
	|	ТЗ.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка
	|ПОМЕСТИТЬ ВТ_ТаблицаЗаполнения
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВИЛС_СоответствияПоРазделамУчета.ЭлементПоиска КАК ЭлементПоиска,
	|	ВИЛС_СоответствияПоРазделамУчета.ЭлементСоответствия КАК ЭлементСоответствия
	|ПОМЕСТИТЬ ВТ_Соответствия
	|ИЗ
	|	РегистрСведений.ВИЛС_СоответствияПоРазделамУчета КАК ВИЛС_СоответствияПоРазделамУчета
	|ГДЕ
	|	ВИЛС_СоответствияПоРазделамУчета.РазрезУчета = Значение(Справочник.ВИЛС_ИменаРазделовСоответствий.СоответствиеСпособовОтраженияДляОценочныхОбязательствПоОтпускам)
	|	И ВИЛС_СоответствияПоРазделамУчета.ЭлементПоиска В(&МассивЭлементов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаЗаполнения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_ТаблицаЗаполнения.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаЗаполнения.Подразделение КАК Подразделение,
	|	ВТ_ТаблицаЗаполнения.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	ВТ_ТаблицаЗаполнения.ДатаНачала КАК ДатаНачала,
	|	ВТ_ТаблицаЗаполнения.ВидОперации КАК ВидОперации,
	|	ВТ_ТаблицаЗаполнения.Начисление КАК Начисление,
	|	ЕСТЬNULL(ВТ_Соответствия.ЭлементСоответствия, ВТ_ТаблицаЗаполнения.СпособОтраженияЗарплатыВБухучете) КАК СпособОтраженияЗарплатыВБухучете,
	|	ВТ_ТаблицаЗаполнения.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВТ_ТаблицаЗаполнения.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТ_ТаблицаЗаполнения.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ВТ_ТаблицаЗаполнения.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	ВТ_ТаблицаЗаполнения.ПериодПринятияРасходов КАК ПериодПринятияРасходов,
	|	ВТ_ТаблицаЗаполнения.Сумма КАК Сумма,
	|	ВТ_ТаблицаЗаполнения.ПФРДоПредельнойВеличины КАК ПФРДоПредельнойВеличины,
	|	ВТ_ТаблицаЗаполнения.ПФРСПревышения КАК ПФРСПревышения,
	|	ВТ_ТаблицаЗаполнения.ПФРПоСуммарномуТарифу КАК ПФРПоСуммарномуТарифу,
	|	ВТ_ТаблицаЗаполнения.ПФРСтраховая КАК ПФРСтраховая,
	|	ВТ_ТаблицаЗаполнения.ПФРНакопительная КАК ПФРНакопительная,
	|	ВТ_ТаблицаЗаполнения.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	ВТ_ТаблицаЗаполнения.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
	|	ВТ_ТаблицаЗаполнения.ПФРНаДоплатуЛетчикам КАК ПФРНаДоплатуЛетчикам,
	|	ВТ_ТаблицаЗаполнения.ПФРНаДоплатуШахтерам КАК ПФРНаДоплатуШахтерам,
	|	ВТ_ТаблицаЗаполнения.ФСС КАК ФСС,
	|	ВТ_ТаблицаЗаполнения.ФССНесчастныеСлучаи КАК ФССНесчастныеСлучаи,
	|	ВТ_ТаблицаЗаполнения.ФФОМС КАК ФФОМС,
	|	ВТ_ТаблицаЗаполнения.ТФОМС КАК ТФОМС,
	|	ВТ_ТаблицаЗаполнения.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки,
	|	ВТ_ТаблицаЗаполнения.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки,
	|	ВТ_ТаблицаЗаполнения.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка,
	|	ВТ_ТаблицаЗаполнения.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка
	|ИЗ
	|	ВТ_ТаблицаЗаполнения КАК ВТ_ТаблицаЗаполнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Соответствия КАК ВТ_Соответствия
	|		ПО ВТ_ТаблицаЗаполнения.СпособОтраженияЗарплатыВБухучете = ВТ_Соответствия.ЭлементПоиска";
	
	ТЗОтбора = НачисленнаяЗарплатаИВзносы.Скопировать();
	ТЗОтбора.Свернуть("СпособОтраженияЗарплатыВБухучете");
	МассивЭлементов = ТЗОтбора.ВыгрузитьКОлонку("СпособОтраженияЗарплатыВБухучете");
	Запрос.УстановитьПараметр("МассивЭлементов",МассивЭлементов);
	Запрос.УстановитьПараметр("ТЗ",НачисленнаяЗарплатаИВзносы);
	НачисленнаяЗарплатаИВзносы = Запрос.Выполнить().Выгрузить();
КонецПроцедуры
