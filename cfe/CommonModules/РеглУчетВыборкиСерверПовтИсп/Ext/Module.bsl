
&ИзменениеИКонтроль("ТекстЗачетАвансов")
Функция ВИЛС_ТекстЗачетАвансов(ДокументИмя)

	#Область АвансыКлиентов

	ТекстЗачетАвансовКлиентов =
	"ВЫБРАТЬ //// Зачет авансов клиентов (Дт <62.2> :: Кт <62.1, 76>)
	|	Операция.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(РеализацияБезПереходаПраваСобственности.ДатаПереходаПраваСобственности, Расчеты.Период) КАК Период,
	|	Аналитика.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Расчеты.ПредоплатаРегл КАК Сумма,
	|	Расчеты.ПредоплатаУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаДт,
	|	(ВЫБОР
	|		КОГДА Операция.Ссылка = Расчеты.ЗаказКлиента ТОГДА Операция.ГруппаФинансовогоУчета
	|		КОГДА НЕ Договоры.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Договоры.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация = ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация <> ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчетаПолучателя
	|		КОГДА НЕ Заказы.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Заказы.ГруппаФинансовогоУчета
	|		КОГДА НЕ Заявки.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Заявки.ГруппаФинансовогоУчета
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказД.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ЗаказД.ГруппаФинансовогоУчета
	//-- НЕ УТКА
	|		КОГДА НЕ ПБДС.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ПБДС.ГруппаФинансовогоУчета
	|		КОГДА НЕ ПКО.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ПКО.ГруппаФинансовогоУчета
	|		КОГДА НЕ ОперацияПоКарте.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ОперацияПоКарте.ГруппаФинансовогоУчета
	|		КОГДА НЕ ПервичныйДокумент.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ПервичныйДокумент.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КОНЕЦ) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	(ВЫБОР
	|		КОГДА Операция.Ссылка = Расчеты.ЗаказКлиента ТОГДА Операция.Подразделение
	|		КОГДА НЕ Договоры.Подразделение ЕСТЬ NULL ТОГДА Договоры.Подразделение
	|		КОГДА НЕ ДоговорыОрганизаций.Подразделение ЕСТЬ NULL ТОГДА ДоговорыОрганизаций.Подразделение
	|		КОГДА НЕ Заказы.Подразделение ЕСТЬ NULL ТОГДА Заказы.Подразделение
	|		КОГДА НЕ Заявки.Подразделение ЕСТЬ NULL ТОГДА Заявки.Подразделение
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказД.Подразделение ЕСТЬ NULL ТОГДА ЗаказД.Подразделение
	//-- НЕ УТКА
	|		КОГДА НЕ ПБДС.Подразделение ЕСТЬ NULL ТОГДА ПБДС.Подразделение
	|		КОГДА НЕ ПКО.Подразделение ЕСТЬ NULL ТОГДА ПКО.Подразделение
	|		КОГДА НЕ ОперацияПоКарте.Подразделение ЕСТЬ NULL ТОГДА ОперацияПоКарте.Подразделение
	|		КОГДА НЕ ПервичныйДокумент.Подразделение ЕСТЬ NULL ТОГДА ПервичныйДокумент.Подразделение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КОНЕЦ) КАК ПодразделениеДт,
	|	Аналитика.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	Аналитика.Контрагент КАК СубконтоДт1,
	|	Аналитика.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Расчеты.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаКт,
	|	(ВЫБОР
	|		КОГДА НЕ Договоры.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Договоры.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация = ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация <> ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчетаПолучателя
	|		КОГДА НЕ Заказы.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Заказы.ГруппаФинансовогоУчета
	|		КОГДА НЕ Заявки.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Заявки.ГруппаФинансовогоУчета
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказД.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ЗаказД.ГруппаФинансовогоУчета
	//-- НЕ УТКА
	|		ИНАЧЕ Операция.ГруппаФинансовогоУчета КОНЕЦ) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Расчеты.Валюта КАК ВалютаКт,
	|	(ВЫБОР
	|		КОГДА НЕ Договоры.Подразделение ЕСТЬ NULL ТОГДА Договоры.Подразделение
	|		КОГДА НЕ ДоговорыОрганизаций.Подразделение ЕСТЬ NULL ТОГДА ДоговорыОрганизаций.Подразделение
	|		КОГДА НЕ Заказы.Подразделение ЕСТЬ NULL ТОГДА Заказы.Подразделение
	|		КОГДА НЕ Заявки.Подразделение ЕСТЬ NULL ТОГДА Заявки.Подразделение
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказД.Подразделение ЕСТЬ NULL ТОГДА ЗаказД.Подразделение
	//-- НЕ УТКА
	|		ИНАЧЕ Операция.Подразделение КОНЕЦ) КАК ПодразделениеКт,
	|	(ВЫБОР
	|		КОГДА НЕ Договоры.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Договоры.НаправлениеДеятельности
	|		КОГДА НЕ ДоговорыОрганизаций.НаправлениеДеятельности ЕСТЬ NULL ТОГДА ДоговорыОрганизаций.НаправлениеДеятельности
	|		КОГДА НЕ Заказы.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Заказы.НаправлениеДеятельности
	|		КОГДА НЕ Заявки.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Заявки.НаправлениеДеятельности
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказД.НаправлениеДеятельности ЕСТЬ NULL ТОГДА ЗаказД.НаправлениеДеятельности
	//-- НЕ УТКА
	|		ИНАЧЕ Операция.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	|	Аналитика.Контрагент КАК СубконтоКт1,
	|	Аналитика.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Расчеты.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Зачет авансов клиентов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументИмя КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Регистратор
	|		И НЕ &НоваяАрхитектураВзаиморасчетов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		Аналитика.КлючАналитики = Расчеты.АналитикаУчетаПоПартнерам
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ДоговорыКонтрагентов КАК Договоры
	|	ПО 
	|		Договоры.Ссылка = Расчеты.ЗаказКлиента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыОрганизаций
	|	ПО 
	|		ДоговорыОрганизаций.Ссылка = Расчеты.ЗаказКлиента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ЗаказКлиента КАК Заказы
	|	ПО 
	|		Заказы.Ссылка = Расчеты.ЗаказКлиента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК Заявки
	|	ПО 
	|		Заявки.Ссылка = Расчеты.ЗаказКлиента
	|	
	//++ НЕ УТКА
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ЗаказДавальца КАК ЗаказД
	|	ПО 
	|		ЗаказД.Ссылка = Расчеты.ЗаказКлиента
	//-- НЕ УТКА
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПБДС
	|	ПО 
	|		ПБДС.Ссылка = Расчеты.ЗаказКлиента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ПриходныйКассовыйОрдер КАК ПКО
	|	ПО 
	|		ПКО.Ссылка = Расчеты.ЗаказКлиента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ОперацияПоПлатежнойКарте КАК ОперацияПоКарте
	|	ПО 
	|		ОперацияПоКарте.Ссылка = Расчеты.ЗаказКлиента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ПервичныйДокумент КАК ПервичныйДокумент
	|	ПО 
	|		ПервичныйДокумент.Ссылка = Расчеты.ЗаказКлиента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.РеализацияТоваровУслуг КАК РеализацияБезПереходаПраваСобственности
	|	ПО 
	|		РеализацияБезПереходаПраваСобственности.Ссылка = Операция.Ссылка
	|		И РеализацияБезПереходаПраваСобственности.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|ГДЕ
	|	Расчеты.ПредоплатаРегл > 0 И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Расчеты.Регистратор <> Расчеты.РасчетныйДокумент
	|	И НЕ Расчеты.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности))";

	#КонецОбласти

	#Область АвансыПоставщикам

	ТекстЗачетАвансовПоставщикам = 
	"ВЫБРАТЬ //// Зачет авансов поставщикам (Дт <60.1, 76> :: Кт <60.2>)
	|	Операция.Ссылка КАК Ссылка,
	|	Расчеты.Период КАК Период,
	|	Аналитика.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Расчеты.ПредоплатаРегл КАК Сумма,
	|	Расчеты.ПредоплатаУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаДт,
	|	(ВЫБОР
	|		КОГДА НЕ Договоры.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Договоры.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация = ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация <> ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчетаПолучателя
	|		КОГДА НЕ Заказы.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Заказы.ГруппаФинансовогоУчета
	|		КОГДА НЕ ЗаказП.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ЗаказП.ГруппаФинансовогоУчета
	|		ИНАЧЕ Операция.ГруппаФинансовогоУчета КОНЕЦ) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	(ВЫБОР
	|		КОГДА НЕ Договоры.Подразделение ЕСТЬ NULL ТОГДА Договоры.Подразделение
	|		КОГДА НЕ ДоговорыОрганизаций.Подразделение ЕСТЬ NULL ТОГДА ДоговорыОрганизаций.Подразделение
	|		КОГДА НЕ Заказы.Подразделение ЕСТЬ NULL ТОГДА Заказы.Подразделение
	|		КОГДА НЕ ЗаказП.Подразделение ЕСТЬ NULL ТОГДА ЗаказП.Подразделение
	|		ИНАЧЕ Операция.Подразделение КОНЕЦ) КАК ПодразделениеДт,
	|	(ВЫБОР
	|		КОГДА НЕ Договоры.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Договоры.НаправлениеДеятельности
	|		КОГДА НЕ ДоговорыОрганизаций.НаправлениеДеятельности ЕСТЬ NULL ТОГДА ДоговорыОрганизаций.НаправлениеДеятельности
	|		КОГДА НЕ Заказы.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Заказы.НаправлениеДеятельности
	|		КОГДА НЕ ЗаказП.НаправлениеДеятельности ЕСТЬ NULL ТОГДА ЗаказП.НаправлениеДеятельности
	|		ИНАЧЕ Операция.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	Аналитика.Контрагент КАК СубконтоДт1,
	|	Аналитика.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Расчеты.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные) КАК ВидСчетаКт,
	|	(ВЫБОР
	|		КОГДА Операция.Ссылка = Расчеты.ЗаказПоставщику ТОГДА Операция.ГруппаФинансовогоУчета
	|		КОГДА НЕ Договоры.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Договоры.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация = ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчета
	|		КОГДА НЕ ДоговорыОрганизаций.ГруппаФинансовогоУчета ЕСТЬ NULL И Аналитика.Организация <> ДоговорыОрганизаций.Организация
	|		ТОГДА ДоговорыОрганизаций.ГруппаФинансовогоУчетаПолучателя
	|		КОГДА НЕ Заказы.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА Заказы.ГруппаФинансовогоУчета
	|		КОГДА НЕ ЗаказП.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ЗаказП.ГруппаФинансовогоУчета
	|		КОГДА НЕ СБДС.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА СБДС.ГруппаФинансовогоУчета
	|		КОГДА НЕ РКО.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА РКО.ГруппаФинансовогоУчета
	|		КОГДА НЕ ПервичныйДокумент.ГруппаФинансовогоУчета ЕСТЬ NULL ТОГДА ПервичныйДокумент.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КОНЕЦ) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Расчеты.Валюта КАК ВалютаКт,
	|	(ВЫБОР
	|		КОГДА Операция.Ссылка = Расчеты.ЗаказПоставщику ТОГДА Операция.Подразделение
	|		КОГДА НЕ Договоры.Подразделение ЕСТЬ NULL ТОГДА Договоры.Подразделение
	|		КОГДА НЕ ДоговорыОрганизаций.Подразделение ЕСТЬ NULL ТОГДА ДоговорыОрганизаций.Подразделение
	|		КОГДА НЕ Заказы.Подразделение ЕСТЬ NULL ТОГДА Заказы.Подразделение
	|		КОГДА НЕ ЗаказП.Подразделение ЕСТЬ NULL ТОГДА ЗаказП.Подразделение
	|		КОГДА НЕ СБДС.Подразделение ЕСТЬ NULL ТОГДА СБДС.Подразделение
	|		КОГДА НЕ РКО.Подразделение ЕСТЬ NULL ТОГДА РКО.Подразделение
	|		КОГДА НЕ ПервичныйДокумент.Подразделение ЕСТЬ NULL ТОГДА ПервичныйДокумент.Подразделение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КОНЕЦ) КАК ПодразделениеКт,
	|	Аналитика.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	|	Аналитика.Контрагент КАК СубконтоКт1,
	|	Аналитика.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Расчеты.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Зачет авансов поставщикам"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументИмя КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Регистратор
	|		И НЕ &НоваяАрхитектураВзаиморасчетов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		Аналитика.КлючАналитики = Расчеты.АналитикаУчетаПоПартнерам
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ДоговорыКонтрагентов КАК Договоры
	|	ПО 
	|		Договоры.Ссылка = Расчеты.ЗаказПоставщику
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыОрганизаций
	|	ПО 
	|		ДоговорыОрганизаций.Ссылка = Расчеты.ЗаказПоставщику
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЗаказПоставщику КАК Заказы
	|	ПО 
	|		Заказы.Ссылка = Расчеты.ЗаказПоставщику
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ЗаказПереработчику КАК ЗаказП
	|	ПО 
	|		ЗаказП.Ссылка = Расчеты.ЗаказПоставщику
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СБДС
	|	ПО 
	|		СБДС.Ссылка = Расчеты.ЗаказПоставщику
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.РасходныйКассовыйОрдер КАК РКО
	|	ПО 
	|		РКО.Ссылка = Расчеты.ЗаказПоставщику
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ПервичныйДокумент КАК ПервичныйДокумент
	|	ПО 
	|		ПервичныйДокумент.Ссылка = Расчеты.ЗаказПоставщику
	|
	|ГДЕ
	|	Расчеты.ПредоплатаРегл > 0. 
	|	И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (Расчеты.Регистратор <> Расчеты.РасчетныйДокумент
	|		ИЛИ Расчеты.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетКомитенту))
	|	И НЕ Расчеты.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхДокументовПоставщику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности))";

	#КонецОбласти

	#Область АвансыКлиентовНоваяАрхитектура

	ТекстЗачетАвансовКлиентовНоваяАрхитектура =
	"ВЫБРАТЬ //// Зачет авансов клиентов (Дт <62.2> :: Кт <62.1, 76>)
	|	Операция.Ссылка КАК Ссылка,
	|	Расчеты.Период КАК Период,
	|	Расчеты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	-Расчеты.ПредоплатаРегл КАК Сумма,
	|	-Расчеты.ПредоплатаУпр  КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаДт,
	|	ЕСТЬNULL(Расчеты.КорГруппаФинансовогоУчета, ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	ЕСТЬNULL(Расчеты.КорПодразделение, ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение)) КАК ПодразделениеДт,
	|	ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Расчеты.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	-Расчеты.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаКт,
	|	ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Расчеты.Валюта КАК ВалютаКт,
	|	ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение) КАК ПодразделениеКт,
	|	ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Операция.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	-Расчеты.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Зачет авансов клиентов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументИмя КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РасчетыСКлиентамиНоваяАрхитектура КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|
	|ГДЕ
	|	Расчеты.ПредоплатаРегл < 0
#Удаление
	|	И Расчеты.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаКлиента)
#КонецУдаления
#Вставка
	|	И Расчеты.ХозяйственнаяОперация В( ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаКлиента), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомиссионера))
#КонецВставки
	|	И НЕ Расчеты.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|	И НЕ Расчеты.Ссылка ССЫЛКА Документ.РасходныйКассовыйОрдер
	|	И НЕ Расчеты.Ссылка ССЫЛКА Документ.ОперацияПоПлатежнойКарте";

	#КонецОбласти

	#Область АвансыПоставщикамНоваяАрхитектура

	ТекстЗачетАвансовПоставщикамНоваяАрхитектура = 
	" 
	|ВЫБРАТЬ //// Зачет авансов поставщикам (Дт <60.1, 76> :: Кт <60.2>)
	|	Операция.Ссылка КАК Ссылка,
	|	Расчеты.Период КАК Период,
	|	Расчеты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	-Расчеты.ПредоплатаРегл КАК Сумма,
	|	-Расчеты.ПредоплатаУпр  КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаДт,
	|	ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение) КАК ПодразделениеДт,
	|	ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Операция.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	-Расчеты.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные) КАК ВидСчетаКт,
	|	ЕСТЬNULL(Расчеты.КорГруппаФинансовогоУчета, ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Расчеты.Валюта КАК ВалютаКт,
	|	ЕСТЬNULL(Расчеты.КорПодразделение, ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение)) КАК ПодразделениеКт,
	|	ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	-Расчеты.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Зачет авансов поставщикам"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументИмя КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РасчетыСПоставщикамиНоваяАрхитектура КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|
	|ГДЕ
	|	Расчеты.ПредоплатаРегл < 0
#Удаление
	|	И Расчеты.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику)
#КонецУдаления
#Вставка
	|	И Расчеты.ХозяйственнаяОперация В( ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомитенту))
#КонецВставки
	|	И НЕ Расчеты.Ссылка ССЫЛКА Документ.ПоступлениеБезналичныхДенежныхСредств
	|	И НЕ Расчеты.Ссылка ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|	И НЕ Расчеты.Ссылка ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|	И НЕ Расчеты.Ссылка ССЫЛКА Документ.ВозвратТоваровПоставщику";

	#КонецОбласти

	Если ДокументИмя = "Документ.ОтчетПоКомиссииМеждуОрганизациями" ИЛИ ДокументИмя = "Документ.ПередачаТоваровМеждуОрганизациями" Тогда
		Отступ = Символы.ПС + Символы.Таб + Символы.Таб + Символы.Таб;
		ВыражениеДляИнтеркомпани = Отступ + "ВЫБОР КОГДА Аналитика.Организация = Операция.Организация" +
		Отступ + "ТОГДА Операция.ГруппаФинансовогоУчета" +
		Отступ + "ИНАЧЕ Операция.ГруппаФинансовогоУчетаПолучателя КОНЕЦ";
		ТекстЗачетАвансовКлиентов = СтрЗаменить(ТекстЗачетАвансовКлиентов, "Операция.ГруппаФинансовогоУчета", ВыражениеДляИнтеркомпани);
		ТекстЗачетАвансовПоставщикам = СтрЗаменить(ТекстЗачетАвансовПоставщикам, "Операция.ГруппаФинансовогоУчета", ВыражениеДляИнтеркомпани);

		ВыражениеДляИнтеркомпани = Отступ + "ВЫБОР КОГДА Расчеты.Организация = Операция.Организация" +
		Отступ + "ТОГДА ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)" +
		Отступ + "ИНАЧЕ ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчетаПолучателя, Операция.ГруппаФинансовогоУчетаПолучателя) КОНЕЦ";
		КорВыражениеДляИнтеркомпани = Отступ + "ВЫБОР КОГДА Расчеты.Организация = Операция.Организация" +
		Отступ + "ТОГДА ЕСТЬNULL(Расчеты.КорГруппаФинансовогоУчета, ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета))" +
		Отступ + "ИНАЧЕ ЕСТЬNULL(Расчеты.КорГруппаФинансовогоУчетаПолучателя, ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчетаПолучателя, Операция.ГруппаФинансовогоУчетаПолучателя)) КОНЕЦ";
		ТекстЗачетАвансовКлиентовНоваяАрхитектура = СтрЗаменить(ТекстЗачетАвансовКлиентовНоваяАрхитектура, "ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)", ВыражениеДляИнтеркомпани);
		ТекстЗачетАвансовПоставщикамНоваяАрхитектура = СтрЗаменить(ТекстЗачетАвансовПоставщикамНоваяАрхитектура, "ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)", ВыражениеДляИнтеркомпани);
		ТекстЗачетАвансовКлиентовНоваяАрхитектура = СтрЗаменить(ТекстЗачетАвансовКлиентовНоваяАрхитектура, "ЕСТЬNULL(Расчеты.КорГруппаФинансовогоУчета, ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета))", КорВыражениеДляИнтеркомпани);
		ТекстЗачетАвансовПоставщикамНоваяАрхитектура = СтрЗаменить(ТекстЗачетАвансовПоставщикамНоваяАрхитектура, "ЕСТЬNULL(Расчеты.КорГруппаФинансовогоУчета, ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета))", КорВыражениеДляИнтеркомпани);
	КонецЕсли;

	МассивТекстовЗапроса = Новый Массив;
	МассивТекстовЗапроса.Добавить(ТекстЗачетАвансовКлиентов);
	МассивТекстовЗапроса.Добавить(ТекстЗачетАвансовПоставщикам);
	МассивТекстовЗапроса.Добавить(ТекстЗачетАвансовКлиентовНоваяАрхитектура);
	МассивТекстовЗапроса.Добавить(ТекстЗачетАвансовПоставщикамНоваяАрхитектура);
	ТекстЗачетАвансов = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстЗачетАвансов = СтрЗаменить(ТекстЗачетАвансов, "ДокументИмя", ДокументИмя);

	Возврат ТекстЗачетАвансов;

КонецФункции
